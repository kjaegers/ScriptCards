!script {{
  --/|Script Name : 5E Action Menu
  --/|Version     : 2.6.8
  --/|Requires SC : 2.7.15+
  --/|Author      : Kurt Jaegers

  --/|IMPORTANT NOTE: Updating this macro via the in-game editor will cause it to exhibit strange behavior. If you wish
  --/|to make settings changes, make them in an external editor (like NOTEPAD) and copy/paste the whole
  --/|macro into the Roll20 editor.

  --/|Description : This script can be used with either a PC or NPC selected. It will generate an action menu with clickable
  --/|buttons to execute character actions and cast spells. The menu is whispered to the player executing the
  --/|script, while the results of clicking the action buttons are displayed in general chat.

  --/|Includes the optional "ActionMenuAddons" library if it is being used
  +++ActionMenuAddons+++
  -->LibAMA_CHECK_FOR_LIBRARY|UseAddons

  --/|=========================================================================================================
  --/|Settings Area - Customize the behavior of the script here

  --&hitPointsBar|1                 --/|What bar do you use for representing hit points for damage application?
  --&skipPreppedSpellsCheck|0       --/|If set to 1, show all known spells for PCs. Otherwise (default) show prepared spells only
  --&skipNPCPrepCheck|1             --/|If set to 1 (Default), show all known spells for NPCs. Otherwise show prepared spells only
  --&trackPCSpellSlots|1            --/|If set to 1, available PC spell slots will be tracked/used.
  --&showPCSpellSlotList|1          --/|If set to 1, a section on the menu will show available spell slots (if the character has them)
  --&requireAmmo|1                  --/|If set to 1, ammo will be required for items that have defined ammo. Create a repeating resource matching the Ammo name
  --&showPCSkillsAndSaves|1         --/|Show saving throw and skill check buttons for PCs (set to 0 to hide)
  --&showNPCSkillsAndSaves|1        --/|Show saving throw and skill check buttons for NPCs (set to 0 to hide)
  --&showPCStandardActions|1        --/|Show "Standard Actions" on PCs (if skills and saves are displayed). Set to 0 to hide
  --&showNPCStandardActions|1       --/|Show "Standard Actions" on NPCs (if skills and saves are displayed). Set to 0 to hide
  --&showCharacterSummary|1         --/|Show a line of summary information at the top of the Action Menu. Set to 0 to disable
  --&showGlobalModifierButtons|1    --/|Show global modifiers (attack, damage, save, skill) buttons and allow players to toggle them on/off. Set to 0 to hide
  --&showBonusActionsAndReactions|1 --/|Show Bonus Actions and Reactions for characters. Set to 0 to disable
  --#functionbenchmarking|0         --/|If set to 1, the console will show performance info after the script is run

  --/|Defaults: 1 for title buttons, 0 for icongraphic and "white-on-black" for icon set. These control how and if some text/images
  --/|            on the Action Menu are replaced with icons from game-icons.net, hosted on my github. In particular, the 
  --/|            TitleButtons setting replaces the emoji's previously used in the title area for settings, short rest, and long rest.
  --/|            the IconsGraphics setting (if 1) replaces "AC", "HP", and "Speed" in the character summary with icons.
  --/|            The type of icons used outside the title area are determiend by the IconsSet - white (white on transparent), 
  --/|            black (black on transparent), or white-on-black.
  --&useGameIconsGraphicsForTitleButtons|1
  --&useGameIconsGraphics|0
  --&gameIconsSet|white-on-black

  --/|If you create a "spell mule" character and put its name here, it's ability list will be checked for
  --/|abilities that match the name of each spell found on PCs. If a match is found, the button that is
  --/|created for that spell will be a sheet-button for that ability instead of a reentrant button back to
  --/|this script for generic processing. This lets you define "special case" macros for non-standard spells.
  --/|The name of the ability on the spell mule needs to exactly match the spell name (case sensitive)
  --&spellmulename|Spell_Mule

  --/|NOTE: Shared Macro Mule functionality is currently broken due to API issues  
  --/|If you create a "shared macro mule" character and put its name here, you can define macros to be used
  --/|generally in your game and a can include a list of macros in a character ability on each character called
  --/|AMLIST that lists each of the mule's abilities that buttons should be created for the in the custom actions section.
  --&sharedmacromulename|Macro_Mule

  --/|If you create a mule character and put its name here, you can define macros to be used for NPC Actions, Reactions,
  --/|Bonus Actions and Legendary Actions. The name matching rules are the same as those for the Spell Mule
  --&sharednpcmacromulename|NPCAbility_Mule
  
  --/|If you create a mule character and put its name here, you can define macros to be used for PC Actions listed in
  --/|"Abilities and Spellcasting" section on the character sheet.
  --&pcabilitymulename|PCAbility_Mule

  --/|=========================================================================================================
  --/| More generalized settings. You may not need to change these.

  --&UseNamedCharacterSettings|0        --/|Set to "1" to enable loading of settings information from a saved format named after the character (case sensitive)
  --&sendGmInfo|0                       --/|If this value is set to 1, attack rolls will have the target's numeric AC value hidden
  --&AllNPCActionsToSender|0            --/|If this value is set to 1, NPC actions (except spellcasting) will be whispered to the sender only and not displayed to everyone
  --&damageApplyScript|damagebuttons    --/|Set this to either "scriptcards", "token-mod", "alterbars", "damagebuttons" or anything else to not apply damage
  --&whisperDamageButtonsToDM|1         --/|If using "damagebuttons" and this is set to 1, the damage buttons will be whispered to the DM instead of being added to the card
  --&whisperDamageButtonResultsToDM|1   --/|If using "damagebuttons" for damage and this value is 1, the DM is whispered a message about damage application
  --&selfHandleAdvantageMode|1          --/|If set to 1, the Action Menu will include advantage/normal/disadvantage toggles (and won't use the character sheet toggle)
  --&defaultAdvantageMode|0             --/|Used with SelfHandleAdvantageMode. 0=Normal, 1=Advantage, 2=Disadvantage
  --&showAdvantageModeText|0            --/|Used with SelfHandleAdvantageMode. 0=Button color coding only. 1=Show "Normal", "Advantage", "Disadvantage" text
  --&compactGlobalModDisplay|1          --/|If set to 1, Global X Mods will use more compact display layouts
  --&EnablePerCharacterConfiguration|1  --/|If set to 1, the Action Menu will create attributes on the character sheet the first time it is executed for a character. It will then use these attributes to generate a configuration menu to allow players to enable/disable some features
  --&UseResistances|1                   --/|If set to 1, damage immunity, vulnerability, and resistance will be included in damage rolls
  --&ConcentrationMarkerName|           --/|If set to a non-blank value, this will be used as the name of the conditional marker to be set on the caster if the cast spell requires concentration
  --&DetectPlayersToken|1               --/|If set to a non-zero value, the script will attempt to resolve a character for a player that runs it without a selected token
  --&ShowRestButtons|1                  --/|If set to 1, rest buttons will be shown in the title bar
  --&Template|none                      --/|If you wish to use Keith Curtis' SuperNotes templates, put the template name here
  --&DisplayButtonsForAMAbilities|1     --/|If set to 1, the menu will include custom buttons defined on characters that include the "AM-" prefix in their names.
  --&UseEmojisToReplaceText|0           --/|If set to 1, emojis will be used in place of text in some places

  --/|Adjusting these values will allow you to customize the padding around the action buttons. The default for Roll20 is 5px. The Action
  --/|menu uses slightly smaller buttons (4px) for actions and very small (2px) padding for global modifier and advantage mode toggle buttons.
  --&defaultButtonPadding|3px
  --&smallButtonPadding|2px
  --&bodyFontSize|12
  --&smallFontSize|11
    
  --/|List spells here, separated by semicolons, that should be kept in the PC Attacks display with "[After Cast]" appended to them
  --~|array;define;SpellsToKeepWithSuffix;Spiritual Weapon;Scorching Ray
  
  --~|array;define;NonScalingCantrips;Eldritch Blast

  --/|List spells here, separated by semicolons, that should be kept in the PC Attacks display with no suffix appended to them
  --~|array;define;SpellsToKeepNoSuffix;
  
  --/|NOTE: This section is experimental and needs further testing. Setting "statusmod_MARKERNAME" allows you to define modifiers that will be applied
  --/|to NPCs based on the status markers active on the NPC. The following modifiers are supported:
  --/| attack:value        (use +X [Desc] to provide a value and description, Can be -X as well) 
  --/| damage:value        (use +X [Desc] to provide a value and description, Can be -X as well)
  --/| rollmode:mode       (use d for disadvantage, a for advantage, n for normal)
  --/| attackermode:mode   (use d for disadvantage, a for advantage, n for normal)
  --/| ac:value            (use +X [Desc] to provide a value and description, Can be -X as well) 
  --/| resist:dmgtypes     (use - to separate damage types, ex bludgeoning-slashing-piercing)
  --/| immune:dmgtypes     (use - to separate damage types, ex bludgeoning-slashing-piercing)
  --/| vuln:dmgtypes       (use - to separate damage types, ex bludgeoning-slashing-piercing)

  --/|For the staus marker mods to work, you will need to set this to 1 and edit/create some modifiers. The items here are just samples.
  --&useTokenMarkersForNPCModifiers|0
  --&statusmod_snail|rollmode:d
  --&statusmod_fist|resist:bludgeoning-piercing-slashing
  --&statusmod_red|damage:+20 [Strong]
  --&statusmod_overdrive|attack:+2 [Haste]
  --&statusmod_tread|ac:-2 [Prone]

  --/|End Settings Area
  --/|=========================================================================================================
  

  --/|=========================================================================================================
  --/|Action Menu Addons Settings
  --?[&UseAddons] -eq "1"|[
    --&PCPostSpellCastFunction|_LibAMA_SpellFumbleOrCrit
    --&PCPostCantripCastFunction|_LibAMA_SpellFumbleOrCrit
    --&PCPostAttackFunction|
    --&NPCPostAttackFunction|
    --&PCSpellListCheck|_LibAMA_SHADOWDARK_SPELL_LIST_CHECK
    --&UseAMAShadowdarkSystem|0
    --?"[&UseAMAShadowdarkSystem]" -eq "1"|&skipPreppedSpellsCheck;1
    --?"[&UseAMAShadowdarkSystem]" -eq "1"|&showPCSpellSlotList;0
  --]|
  --/|Action Menu Addons Settings Area
  --/|=========================================================================================================
  

  -->CHECK_VERSION_INFO|

  --/|Determine if a token is selected or if we need to (and can based on config) automatically find a token for this player.
  --?"[@SC_SelectedTokens()]" -eq "undefined array" -and [&DetectPlayersToken] -ne 1|REPORT_NEED_SELECTED_TOKEN
  --?"[@SC_SelectedTokens()]" -ne "undefined array" -and "[@SC_SelectedTokens()]" -eq "1"|BEGIN_WITH_SC_SELECTED_TOKENS
  --?"[@SC_SelectedTokens()]" -gt 1|REPORT_MULTIPLE_SELECTED_TOKEN
  --?[&DetectPlayersToken] -ne 1 -and "[@SC_SelectedTokens()]" -ne "1"|REPORT_NEED_SELECTED_TOKEN
  --?[&DetectPlayersToken] -ne 1 -and "[@SC_SelectedTokens()]" -eq "1"|BEGIN_WITH_SC_SELECTED_TOKENS
  --?[&SendingPlayerIsGM] -eq 1 -and "[@SC_SelectedTokens()]" -ne "1"|REPORT_NEED_SELECTED_TOKEN

  -->GET_CONTROLLED_TOKENS_FROM_PAGETOKENS|saveTokenID
  --?"[&saveTokenID]" -eq ""|REPORT_NO_TOKENS_ON_PAGE
  --?"[&saveTokenID]" -inc ","|REPORT_MULTIPLE_POSSIBLE_TOKENS
  --^SKIP_SELECTED_TOKENS|

  --:BEGIN_WITH_SC_SELECTED_TOKENS|
  --&saveTokenID|[@SC_SelectedTokens(0)]
  --:SKIP_SELECTED_TOKENS|

  --/|Give the script a reentrant name based on the selected character id so we can have multiple instances at the same time
  --#reentrant|CharacterAction [&saveTokenID]
  --&QB|? --&QB|+{ --&QE|}
  --&AB|@ --&AB|+{
  --&TB|& --&TB|+# --&TB|+64 --&TB|+; --&TB|+{
  --&BED|& --&BED|+# --&BED|+128719;
  --&CLOCK|& --&CLOCK|+# --&CLOCK|+128336;
  --&GEAR|& --&GEAR|+# --&GEAR|+x2699;
  --&SPACE|X X --&SPACE|[&SPACE(1,1)]
  --&NB|& --&NB|+nbsp --&NB|+;
  --&obrac|[ --&cbrac|]
  --&vbar||

  --&ShortRestImage|[&CLOCK]
  --&LongRestImage|[&BED]
  --&SettingsImage|[&GEAR]
  --&ACImage|[b]AC:[&NB][/b]
  --&HPImage|[b]HP:[&NB][/b]
  --&SpeedImage|[b]Speed:[&NB][/b]
  --&SkillCheckImage|Skill
  --&ToolCheckImage|Tool
  --&SavingThrowImage|Save
  --&RepeatImage|Repeat

  --/|Useful Emoticons
  --&emoSpeed|& --&emoSpeed|+# --&emoSpeed|+9193;
  --&emoShield|& --&emoShield|+# --&emoShield|+129355;
  --&emoHeart|& --&emoHeart|+# --&emoHeart|+129505;
  --&emoShield|[img width=25 height=25]https://s3.amazonaws.com/files.d20.io/images/30/JAFfatM7Ius91Eg4uPBY6w/icon.png?1575370642[/img]
  --?[&useGameIconsGraphicsForTitleButtons] -eq 0|SKIP_GAME_ICONS_FOR_TITLES
  --&gameIconsBasePath|https://raw.githubusercontent.com/kjaegers/game-icons/master/transparent/
  --&ShortRestImage|[img width=15 height=15][&gameIconsBasePath]white/meditation.png[/img]
  --&LongRestImage|[img width=15 height=15][&gameIconsBasePath]white/night-sleep.png[/img]
  --&SettingsImage|[img width=15 height=15][&gameIconsBasePath]white/cog.png[/img]
  --&RepeatImage|[img width=15 height=15][&gameIconsBasePath]white/anticlockwise-rotation.png[/img]
  --:SKIP_GAME_ICONS_FOR_TITLES|
  --?[&useGameIconsGraphics] -eq 0|SKIP_GAME_ICONS
  --/&SkillCheckImage|[img width=15 height=15][&gameIconsBasePath]white/skills.png[/img]
  --/&SavingThrowImage|[img width=15 height=15][&gameIconsBasePath]white/dodging.png[/img]
  --&ACImage|[img width=20 height=20][&gameIconsBasePath][&gameIconsSet]/shield.png[/img][&NB]
  --&HPImage|[img width=20 height=20][&gameIconsBasePath][&gameIconsSet]/health-normal.png[/img][&NB]
  --&SpeedImage|[img width=20 height=20][&gameIconsBasePath][&gameIconsSet]/walk.png[/img][&NB]
  --:SKIP_GAME_ICONS|

  --/|Set up some style information for the action menu and subsequent cards. These attempt to somewhat
  --/|simulate the 5E style
  --#bodyfontface|Helvetica
  --#oddrowfontcolor|#290000
  --#evenrowfontcolor|#290000
  --#bodyfontsize|[&bodyFontSize]px
  --#buttonfontsize|10px
  --#buttonbackground|#930c10
  --#buttonbackground|#672223
  --#buttonbackground|rgba(103,34,35,1); display:inline-block;
  --/#buttonbordercolor|rgba(0,0,0,0)
  --/#buttonbackground|rgba(0,0,0,0); display:inline-block; border:none
  --/#buttontextcolor|#930c10
  --#buttonfontface|Tahoma
  --#titlecardbackgroundcolor|#930c10
  --#titlecardbackgroundcolor|#672223
  --#titlefontcolor|#fffebd
  --#titlefontsize|1.5em
  --#titlefontshadow|0
  --#oddrowbackground|#f7ce65
  --#evenrowbackground|#f7ce65
  --#oddrowbackground|#b3ab96
  --#evenrowbackground|#b3ab96
  --#subtitlefontcolor|#fffebd
  --#tablebgcolor|#fffebd
  --#lineheight|10pt
  --#buttonpadding|[&defaultButtonPadding]
  --#overridetemplate|[&Template]
  --&GAM_Button_Active_Color|009900
  --&GAM_Button_Inactive_Color|672223
  
  --&AMOriginalSendingPlayer|[&SendingPlayerID]

  --/|The menu is whispered to the player executing the macro. #whisper will be set again later for the
  --/|actual output to be public.
  --#whisper|self

  --/|Store the selected character so we can use it later even if it gets de-selected
  --#sourcetoken|[&saveTokenID]

  --?[&UseNamedCharacterSettings] -eq 1|[
    --lsettings|[*S:character_name]
  --]|

  --/|Determine damage subroutine. Start by assuming we aren't going to apply damage
  --&damageRoutine|DontApplyDamage
  --?[&damageApplyScript] -eq "scriptcards"|&damageRoutine;ApplyDamageScriptCards
  --?[&damageApplyScript] -eq "token-mod"|&damageRoutine;ApplyDamageTokenmod
  --?[&damageApplyScript] -eq "alterbars"|&damageRoutine;ApplyDamageAlterbars
  --?[&damageApplyScript] -eq "damagebuttons"|&damageRoutine;SHOW_DAMAGE_BUTTONS

  --#emotestate|hidden
  --#title|[*S:character_name]

  --:AFTER_ADVMODE_CHANGE|

  --#leftsub|Action Menu

  --?[&EnablePerCharacterConfiguration] -eq 0|SKIP_PER_CHAR_SETTINGS
  --#leftsub|Action Menu [rbutton][&SettingsImage]::DISPLAY_CHARACTER_CONFIG_MENU[/rbutton]
  -->LOAD_PER_CHARACTER_CONFIG_OPTIONS|[*S:character_id]
  --:SKIP_PER_CHAR_SETTINGS|
  
  --?[*S:npc] -eq 1 -or [&ShowRestButtons] -ne 1|SKIP_REST_BUTTONS
  --?"[&perCharacterOptions]" -inc "$SHRB0$"|SKIP_REST_BUTTONS
  --#rightsub|Rest: [rbutton][&ShortRestImage]::DO_SHORT_REST[/rbutton][rbutton][&LongRestImage]::DO_LONG_REST[/rbutton]
  --:SKIP_REST_BUTTONS|

  --/|Determine if the selected character is a PC or NPC and process appropriately
  --?[*S:npc] -eq 1|proc_npc|proc_pc

  --/|=========================================================================================================
  --/|Handle Action Menu creation for NPCs

  --:proc_npc|
  --/+|[hr #58180d]

  -->BuildCharacterSummary|[*S:t-id];CharSummaryText;npc_ac
  
  --?[&showCharacterSummary] -ne 1 -or "[&perCharacterOptions]" -inc "$SCS0$" |SKIP_NPC_SUMMARY
  --/+|[c][F[&smallFontSize]][&CharSummaryText][/F][/c]
  --+|[&CharSummaryText]
  --:SKIP_NPC_SUMMARY|
  
  --&AdvModeText|

  --?[&selfHandleAdvantageMode] -eq 1|[
    --?"[&currentAdvantageMode]" -eq ""|&currentAdvantageMode;[&defaultAdvantageMode]
    --&mode|
    --?[&showAdvantageModeText] -eq 0|SKIP_NPC_SET_MODE
    --C[&currentAdvantageMode]|0:&mode; Normal|1:&mode; Advantage|2:&mode; Disadvantage
    --:SKIP_NPC_SET_MODE|
    --&nbuttoncolor|#[&GAM_Button_Inactive_Color]
    --&abuttoncolor|#[&GAM_Button_Inactive_Color]
    --&dbuttoncolor|#[&GAM_Button_Inactive_Color]
    --C[&currentAdvantageMode]|0:&nbuttoncolor;#[&GAM_Button_Active_Color]|1:&abuttoncolor;#[&GAM_Button_Active_Color]|2:&dbuttoncolor;#[&GAM_Button_Active_Color]
    --#buttonpadding|[&smallButtonPadding]
    --&AdvModeText|[r][b]Attack Mode:[/b] [&mode][rbutton:#FFFFFF:[&nbuttoncolor]]N::SET_ADV_MODE;0[/rbutton][rbutton:#FFFFFF:[&abuttoncolor]]A::SET_ADV_MODE;1[/rbutton][rbutton:#FFFFFF:[&dbuttoncolor]]D::SET_ADV_MODE;2[/rbutton][/r]    --C[&currentAdvantageMode]|0:&mode;Normal|1:&mode;Advantage|2:&mode;Disadvantage
    --#buttonpadding|[&defaultButtonPadding]
  --]|

  --/|Skill Checks and Saving Throws
  --&savesAndSkills|
  --?[&showNPCSkillsAndSaves] -eq 1 -or "[&perCharacterOptions]" -inc "$SSAS1$"|>BUILD_SAVES_AND_SKILLS_BUTTONS;savesAndSkills
  --/+|[t width='100%'][tr][td align=left][&savesAndSkills][/td][td align=right][&AdvModeText][/td][/tr][tr][td][&NB][/td][td][/td][tr][/t]
  --+|[t width='100%'][tr][td align=left][&savesAndSkills][/td][td align=right][&AdvModeText][/td][/tr][/t][F5][BR][/F]

  --/|5E Sheet Specific BEGIN

  --&HighestSlot|9
  --?[&trackPCSpellSlots] -eq 1|[
    -->GET_HIGHEST_LEVEL_SPELL_SLOT|HighestSlot
  --]|
  -->BUILD_BUTTON_LIST|[*S:character_id];repeating_npcaction;name;EXEC_NPC_ACTION;Actions;[&sharednpcmacromulename];0
  -->BUILD_BUTTON_LIST|[*S:character_id];repeating_npcbonusaction;name;EXEC_NPC_BONUS_ACTION;Bonus Actions;[&sharednpcmacromulename];0
  -->BUILD_BUTTON_LIST|[*S:character_id];repeating_npcreaction;name;EXEC_NPC_REACTION;Reactions;[&sharednpcmacromulename];0
  -->BUILD_BUTTON_LIST|[*S:character_id];repeating_npcaction-l;name;EXEC_NPC_LEGENDARY_ACTION;Legendary Actions;[&sharednpcmacromulename];0
  -->BUILD_SPELL_BUTTON_LIST|[*S:character_id];repeating_spell-cantrip;spellname;EXEC_PC_CANTRIP;Cantrips;[&sharednpcmacromulename];0
  --%levelCounter|1;9
    --/\|repeating_spell repeating_spell-[&levelCounter]
    --/\|Spellname-[&spellname]
    -->BUILD_SPELL_BUTTON_LIST|[*S:character_id];repeating_spell-[&levelCounter];spellname;EXEC_PC_SPELL;Level [&levelCounter] Spells;[&spellmulename];[&levelCounter]
  --%|
  --/|5E Sheet Specific END

  --#buttonpadding|[&smallButtonPadding]
  --X|

  --/|=========================================================================================================
  --/|Reentrant points for NPCs. Most of the reentry points set up variables and jump back into the "NPCA_NOT_ATTACK" portion
  --/|of the EXEC_NPC_ACTION code. Note this this code is 5E specific, so you will likely need to replace the entire
  --/|"EXEC_*" sections to carry out specific actions if building for a different sheet

  --:EXEC_NPC_ACTION|action_name
  --?[&AMOriginalSendingPlayer] -ne [&SendingPlayerID]|YOU_CANNOT_DO_THAT
  --&whisperTarget|
  --?[*S:npc] -eq 1 -and [&AllNPCActionsToSender] -eq 1||&whisperTarget;self
  --#whisper|[&whisperTarget]
  --#emotestate|visible
  --~action_name|string;before;|;[&reentryval]
  --&action_name|[&action_name(replace,-!-!-,;)]
  --~action_target|string;after;|;[&reentryval]
  --#targettoken|[&action_target]
  --#leftsub|[*S:character_name]
  --#rightsub|
  --
  --#emotetext|[*S:t-name] uses [&action_name] against [*T:t-name]
  --#parameterdelimiter|$$$
  --Rfind|[*S:character_id]$$$[&action_name]$$$repeating_npcaction$$$name
  --#parameterdelimiter|;
  --?"[*R:name]" -ne "NoRepeatingAttributeLoaded"|[
    --#leftsub|[*R:name]
    --?"[*R:attack_flag]" -ne on|NPCA_NOT_ATTACK
    --:NPCA_MAKE_AN_ATTACK|
    --#rightsub|[rbutton][&RepeatImage]::EXEC_NPC_ACTION;[&action_name]|[&TB]target|token_id}[/rbutton]
    --&rollType|1d20
    --?"[*S:advantagetoggle]" -inc advantage|&rollType;2d20kh1
    --?"[*S:advantagetoggle]" -inc disadvantage|&rollType;2d20kl1
    -->DETERMINE_NPC_STATUS_MODIFIERS|[*S:t-id];A
    -->DETERMINE_NPC_STATUS_MODIFIERS|[*T:t-id];T 
    --?[&selfHandleAdvantageMode] -eq 1 -and [&currentAdvantageMode] -eq 0|&rollType;1d20
    --?[&selfHandleAdvantageMode] -eq 1 -and [&currentAdvantageMode] -eq 1|&rollType;2d20kh1
    --?[&selfHandleAdvantageMode] -eq 1 -and [&currentAdvantageMode] -eq 2|&rollType;2d20kl1
    --?"[&ANPC_ROLLMODE]" -ne "" -and "[&TNPC_ATTACKAGAINSTMODE]" -eq ""|SKIP_NPC_ATTACK_ROLLMODE_CALC
    --?"[&ANPC_ROLLMODE]" -ne "" -and "[&TNPC_ATTACKAGAINSTMODE]" -eq ""|&rollType;[&ANPC_ROLLMODE]
    --?"[&ANPC_ROLLMODE]" -ne "" -and "[&TNPC_ATTACKAGAINSTMODE]" -eq ""|SKIP_NPC_ATTACK_ROLLMODE_CALC
    --?"[&ANPC_ROLLMODE]" -eq "" -and "[&TNPC_ATTACKAGAINSTMODE]" -ne ""|&rollType;[&TNPC_ATTACKAGAINSTMODE]
    --?"[&ANPC_ROLLMODE]" -eq "" -and "[&TNPC_ATTACKAGAINSTMODE]" -ne ""|SKIP_NPC_ATTACK_ROLLMODE_CALC
    --?"[&ANPC_ROLLMODE]" -eq "1d20" -and "[&TNPC_ROLLMODE]" -eq "1d20"|&rollType;1d20
    --?"[&ANPC_ROLLMODE]" -eq "2d20kh1" -and "[&TNPC_ROLLMODE]" -eq "1d20"|&rollType;2d20kh1
    --?"[&ANPC_ROLLMODE]" -eq "2d20kl1" -and "[&TNPC_ROLLMODE]" -eq "1d20"|&rollType;2d20kl1
    --?"[&ANPC_ROLLMODE]" -eq "2d20kh1" -and "[&TNPC_ROLLMODE]" -eq "2d20kh1"|&rollType;2d20kh1
    --?"[&ANPC_ROLLMODE]" -eq "2d20kl1" -and "[&TNPC_ROLLMODE]" -eq "2d20kl1"|&rollType;2d20kl1
    --?"[&ANPC_ROLLMODE]" -eq "2d20kh1" -and "[&TNPC_ROLLMODE]" -eq "2d20kl1"|&rollType;1d20
    --?"[&ANPC_ROLLMODE]" -eq "2d20kl1" -and "[&TNPC_ROLLMODE]" -eq "2d20kh1"|&rollType;1d20
    --:SKIP_NPC_ATTACK_ROLLMODE_CALC|
    --=ToHit|[&rollType] + [*R:attack_tohit] [&ANPC_ATKMOD]
    --&dmgModType1|
    --&dmgModType2|
    --?"[*T:npc]" -ne "1" -or [&UseResistances] -eq 0|SKIP_NPC_DMG_MODS
    -->CheckDamageModifiersNPC|[*T:character_id];dmgModType1;[*R:attack_damagetype];0;0;0;T
    -->CheckDamageModifiersNPC|[*T:character_id];dmgModType2;[*R:attack_damagetype2];0;0;0;T
    --:SKIP_NPC_DMG_MODS|
    --=AC|[*T:ac]
    --?"[*T:npc_ac]" -ne ""|=AC;[*T:npc_ac]
    --?[&sendGmInfo] -eq 1|[
      --+[*R:atkname] Attack|rolled [$ToHit] vs the target's AC[br][br]
      --*[*R:atkname] Attack|rolled [$ToHit] vs AC [b][$AC.Raw][/b]
    --]|[
      --+[*R:atkname] Attack|rolled [$ToHit] vs AC [b][$AC.Raw][/b][br][br]
    --]|    
    --?[$ToHit.Base] -eq 20|NPCA_CRIT
    --?[$ToHit.Base] -eq 1|NPCA_MISS
    --?[$ToHit.Total] -lt [$AC]|NPCA_MISS
    --?"[*R:attack_damage2]" -ne ""|NPCA_HIT_TWODAMAGE
    --=Damage|[*R:attack_damage] [&ANPC_DMGMOD] [&dmgModType1]
    --+Hit|[$Damage] [*R:attack_damagetype]
    --+|[*R:description]
    -->[&damageRoutine]|[&action_target];[&hitPointsBar];-[$Damage.Raw]
    --^NPCA_DONE|
    --:NPCA_HIT_TWODAMAGE|
    --=Damage|[*R:attack_damage] [&ANPC_DMGMOD] [&dmgModType1]
    --=Damage2|[*R:attack_damage2] [&ANPC_DMGMOD] [&dmgModType2]
    --=TotalDamage|[$Damage.Raw] + [$Damage2.Raw]
    --+Hit|[$Damage] [*R:attack_damagetype] and [$Damage2] [*R:attack_damagetype2]
    --+|[*R:description]
    -->[&damageRoutine]|[&action_target];[&hitPointsBar];-[$TotalDamage.Raw]
    --^NPCA_DONE|

    --:NPCA_CRIT|
    --?"[*R:attack_damage2]" -ne ""|NPCA_CRIT_TWODAMAGE
    --=Damage|[*R:attack_damage] + [*R:attack_crit] [&ANPC_DMGMOD] [&dmgModType1]
    -->[&damageRoutine]|[&action_target];[&hitPointsBar];-[$Damage.Raw]
    --+Critical|[$Damage] [*R:attack_damagetype]
    --+|[*R:description]
    --^NPCA_DONE|
    
    --:NPCA_CRIT_TWODAMAGE|
    --=Damage|[*R:attack_damage] + [*R:attack_crit] [&ANPC_DMGMOD] [&dmgModType1]
    --=Damage2|[*R:attack_damage2] + [*R:attack_crit2] [&ANPC_DMGMOD] [&dmgModType2]
    --=TotalDamage|[$Damage.Raw] + [$Damage2.Raw]    
    --+Critical|[$Damage] [*R:attack_damagetype] and [$Damage2] [*R:attack_damagetype2]
    --+|[*R:description]
    -->[&damageRoutine]|[&action_target];[&hitPointsBar];-[$TotalDamage.Raw]

    --:NPCA_MISS|
    --+Miss|The attack missed.
    --^NPCA_DONE|

    --:NPCA_NOT_ATTACK|
    --+|[*R:description]
    
    --:NPCA_DONE|
    --?"[&NPCPostAttackFunction]" -ne ""|>[&NPCPostAttackFunction]
    --]|
  --X|

  --:EXEC_NPC_BONUS_ACTION|action_name
  --~action_name|string;before;|;[&reentryval]
  --~action_target|string;after;|;[&reentryval]
  --&whisperTarget|
  --?[*S:npc] -eq 1 -and [&AllNPCActionsToSender] -eq 1||&whisperTarget;self
  --#whisper|[&whisperTarget]
  --#emotestate|visible
  --#targettoken|[&action_target]
  --#emotetext|[*S:t-name] uses [&action_name] against [*T:t-name]
  --Rfind|[*S:character_id];[&action_name];repeating_npcbonusaction;name
  --#leftsub|[*R:name]
  --#rightsub|
  --?"[*R:attack_flag]" -eq on|NPCA_MAKE_AN_ATTACK
  --^NPCA_NOT_ATTACK|
  --X|

  --:EXEC_NPC_REACTION|action_name
  --~action_name|string;before;|;[&reentryval]
  --~action_target|string;after;|;[&reentryval]
  --&whisperTarget|
  --?[*S:npc] -eq 1 -and [&AllNPCActionsToSender] -eq 1||&whisperTarget;self
  --#whisper|[&whisperTarget]
  --#emotestate|visible
  --#targettoken|[&action_target]
  --#emotetext|[*S:t-name] uses [&action_name] against [*T:t-name]
  --Rfind|[*S:character_id];[&action_name];repeating_npcreaction;name
  --#leftsub|[*R:name]
  --#rightsub|
  --?"[*R:attack_flag]" -eq on|NPCA_MAKE_AN_ATTACK
  --^NPCA_NOT_ATTACK|
  --X|

  --:EXEC_NPC_LEGENDARY_ACTION|action_name
  --&whisperTarget|
  --?[*S:npc] -eq 1 -and [&AllNPCActionsToSender] -eq 1||&whisperTarget;self
  --#whisper|[&whisperTarget]
  --#emotestate|visible
  --~action_name|string;before;|;[&reentryval]
  --~action_target|string;after;|;[&reentryval]
  --#targettoken|[&action_target]
  --#emotetext|[*S:t-name] uses [&action_name] against [*T:t-name]  
  --Rfind|[*S:character_id];[&action_name];repeating_npcaction-l;name
  --#leftsub|[*R:name]
  --#righsub|
  --?"[*R:attack_flag]" -eq on|NPCA_MAKE_AN_ATTACK
  --^NPCA_NOT_ATTACK|
  --X|  

  --/|=========================================================================================================
  --/|Handle action menu creation for PCs. 

  --:proc_pc|
  --/+|[hr #58180d]
  --+| 
  --#whisper|self

  -->FIND_CLASS_LEVEL|Sorcerer;SorcererLevel
  -->FIND_CLASS_LEVEL|Warlock;WarlockLevel
  -->FIND_CLASS_LEVEL|Wizard;WizardLevel
  -->FIND_CLASS_LEVEL|Bard;BardLevel
  -->FIND_CLASS_LEVEL|Druid;DruidLevel
  -->FIND_CLASS_LEVEL|Cleric;ClericLevel
  -->FIND_CLASS_LEVEL|Ranger;RangerLevel
  -->FIND_CLASS_LEVEL|Paladin;PaladinLevel
  -->FIND_CLASS_LEVEL|Fighter;FighterLevel
  -->FIND_CLASS_LEVEL|Barbarian;BarbarianLevel
  -->FIND_CLASS_LEVEL|Monk;MonkLevel
  -->FIND_CLASS_LEVEL|Rogue;RogueLevel
  -->FIND_CLASS_LEVEL|Artificer;ArtificerLevel
  
  --&WizLevel|[&WizardLevel]
  --&SorcLevel|[&SorcererLevel]
    
  -->BuildCharacterSummary|[*S:character_id];CharSummaryText;ac

  --?[&showCharacterSummary] -ne 1 -or "[&perCharacterOptions]" -inc "$SCS0$" |SKIP_PC_SUMMARY
  --+|[c][F[&smallFontSize]][&CharSummaryText][/F][/c]
  --:SKIP_PC_SUMMARY|

   --&AdvModeText|
  --?[&selfHandleAdvantageMode] -eq 1|[
    --?"[&currentAdvantageMode]" -eq ""|&currentAdvantageMode;[&defaultAdvantageMode]
    --&mode|
    --?[&showAdvantageModeText] -eq 0|SKIP_PC_SET_MODE
    --C[&currentAdvantageMode]|0:&mode; Normal|1:&mode; Advantage|2:&mode; Disadvantage
    --:SKIP_PC_SET_MODE|
    --&nbuttoncolor|#[&GAM_Button_Inactive_Color]
    --&abuttoncolor|#[&GAM_Button_Inactive_Color]
    --&dbuttoncolor|#[&GAM_Button_Inactive_Color]
    --C[&currentAdvantageMode]|0:&nbuttoncolor;#[&GAM_Button_Active_Color]|1:&abuttoncolor;#[&GAM_Button_Active_Color]|2:&dbuttoncolor;#[&GAM_Button_Active_Color]
    --#buttonpadding|[&smallButtonPadding]
    --&AdvModeText|[r][b]Attack Mode:[/b] [&mode][rbutton:#FFFFFF:[&nbuttoncolor]]N::SET_ADV_MODE;0[/rbutton][rbutton:#FFFFFF:[&abuttoncolor]]A::SET_ADV_MODE;1[/rbutton][rbutton:#FFFFFF:[&dbuttoncolor]]D::SET_ADV_MODE;2[/rbutton][/r]    --C[&currentAdvantageMode]|0:&mode;Normal|1:&mode;Advantage|2:&mode;Disadvantage
    --#buttonpadding|[&defaultButtonPadding]
  --]|

  --/|Skill Checks and Saving Throws
  --&savesAndSkills|
  --?[&showPCSkillsAndSaves] -eq 1 -and "[&perCharacterOptions]" -ninc "$SSAS0$"|>BUILD_SAVES_AND_SKILLS_BUTTONS;savesAndSkills

  --#buttonpadding|[&smallButtonPadding]
  --/+|[t width='100%'][tr][td align=left][&savesAndSkills][/td][td align=right][&AdvModeText][/td][/tr][tr][td][&NB][/td][td][/td][tr][/t]
  --+|[t width='100%'][tr][td align=left][&savesAndSkills][/td][td align=right][&AdvModeText][/td][/tr][/t][p style='font-size:smaller; margin-top:0px; margin-bottom:0px; line-height:6px;'][&NB][/p]
  --#buttonpadding|[&defaultButtonPadding]

  --?[&showGlobalModifierButtons] -eq 1 -and "[&perCharacterOptions]" -ninc "$SGMB0$" |[
    -->Create_Global_Attack_Modifier_Buttons|[*S:character_id];gambuttons
    -->Create_Global_Damage_Modifier_Buttons|[*S:character_id];gdmbuttons
    -->Create_Global_Save_Modifier_Buttons|[*S:character_id];gsmbuttons
    -->Create_Global_AC_Modifier_Buttons|[*S:character_id];gacmbuttons
    -->Create_Global_Skill_Modifier_Buttons|[*S:character_id];gskmbuttons
    --#buttonpadding|[&smallButtonPadding]
    --+|[p style='text-align:center; font-weight:bold; margin-bottom:0px;']- - - - G L O B A L [&NB][&NB] M O D S - - - -[/p]
    --+|[t width='100%'][tr][td width='25%' align='center'][b]ATK[/b][/td][td width='25%' align='center'][b]DMG[/b][/td][td width='25%' align='center'][b]AC[/b][/td][td width='25%' align='center'][b]SAVE[/b][/td][/tr][tr][td width='25%' align='center'][&gambuttons][/td][td width='25%' align='center'][&gdmbuttons][/td][td width='25%' align='center'][&gacmbuttons][/td][td width='25%' align='center'][&gsmbuttons][/td][/tr][/t]
  --]| 

  -->BUILD_BUTTON_LIST|[*S:character_id];repeating_attack;atkname;EXEC_PC_ATTACK;Attacks and Actions;[&pcabilitymulename];0;1
  --?"[&showBonusActionsAndReactions]" -eq 1|>DISPLAY_PC_OTHER_ACTIONS
  -->BUILD_SPELL_BUTTON_LIST|[*S:character_id];repeating_spell-cantrip;spellname;EXEC_PC_CANTRIP;Cantrips;[&spellmulename];0
  
  --/|Display a grid of the available spell slots for this character
  --&HighestSlot|9
  --?[&trackPCSpellSlots] -eq 1|[
    -->GET_HIGHEST_LEVEL_SPELL_SLOT|HighestSlot
    --&spellSlotInfo1|[t align=center width=100% border=1][tr][td][b]Level[/b][/td]
    --&spellSlotInfo2|[/tr][tr][td][b]Slots[/b][/td]
    --&anySpell|0
    --%levelCounter|1;9
      -->GET_AVAILABLE_SPELL_SLOTS|[&levelCounter];slotCount
      --/?[*S:lvl[&levelCounter]_slots_total] -gt 0|&spellSlotInfo;+L[&levelCounter]([&slotCount]) 
      --?[*S:lvl[&levelCounter]_slots_total] -gt 0|&spellSlotInfo1;+[td align=center][b][i][&levelCounter][/i][/b][/td] 
      --?[*S:lvl[&levelCounter]_slots_total] -gt 0|&spellSlotInfo2;+[td align=center][&slotCount][/td] 
      --?[*S:lvl[&levelCounter]_slots_total] -gt 0|&anySpell;1 
    --%|
    --?[&anySpell] -eq 0|SKIP_SLOT_DISPLAY
    --?[&showPCSpellSlotList] -eq 0|SKIP_SLOT_DISPLAY
    --+|[c][b]Available Spell Slots[/b][/c][&spellSlotInfo1][&spellSlotInfo2][/tr][/t]
    --:SKIP_SLOT_DISPLAY|
  --]|

  --/|The PC's level 1 thru 9 spells
  --%levelCounter|1;9
    -->BUILD_SPELL_BUTTON_LIST|[*S:character_id];repeating_spell-[&levelCounter];spellname;EXEC_PC_SPELL;Level [&levelCounter] Spells;none;[&levelCounter]    
  --%|
  --?[&DisplayButtonsForAMAbilities] -eq 1|>DISPLAY_ACTIONMENU_CHARACTER_ABILITIES;[*S:character_id]
  --#buttonpadding|[&smallButtonPadding]
  --X|

  --/|=========================================================================================================
  --/|Reentrant points for PC actions

  --:EXEC_PC_ATTACK|
  --?[&AMOriginalSendingPlayer] -ne [&SendingPlayerID]|YOU_CANNOT_DO_THAT
  --/+|[hr #58180d]
  --#whisper|
  --#emotestate|visible
  --&vfx|
  --&sfx|
  --~attack_name|string;before;|;[&reentryval]
  --~attack_target|string;after;|;[&reentryval]
  --#targettoken|[&attack_target]
  -->DETERMINE_NPC_STATUS_MODIFIERS|[*T:t-id];T
  --/\|Emote in PC Attack|[*S:t-name] uses [&attack_name] against [*T:t-name]
  --#emotetext|[*S:t-name] uses [&attack_name] against [*T:t-name]
  -->Find_Active_Global_Attack_Modifiers|[*S:character_id];PC_GAM
  -->Find_Active_Global_Damage_Modifiers|[*S:character_id];PC_GDM
  -->Find_Active_Global_Damage_Modifiers|[*S:character_id];PC_GDM_CRIT;1
  --Rfind|[*S:character_id];[&attack_name];repeating_attack;atkname
  --?[*R:attack_flag] -ne 1|
  --?[&requireAmmo] -eq 0|NoAmmoNeededPC_Attack
  --?"[*R:ammo]" -eq ""|NoAmmoNeededPC_Attack
  --~ResourceUsed|string;before;|;[*R:ammo]
  -->FIND_REPEATING_RESOURCE|[*S:character_id];[&ResourceUsed];AmmoAttrName;AmmoAttributeID;AmmoCount
  --?[&AmmoCount] -lt 1|_NO_AMMO_AVAILABLE
  --:NoAmmoNeededPC_Attack|
  --#title|[*R:atkname]
  --#leftsub|[*S:character_name]
  --#rightsub|[rbutton][&RepeatImage]::EXEC_PC_ATTACK;[&attack_name]|[&TB]target|token_id}[/rbutton]
  --~|array;fromstring;fx;$;[*R:atk_desc]
  --%loop|foreach;fx
    --?"[&loop]" -inc "sfx:"|&sfx;[&loop(after,:)]
    --?"[&loop]" -inc "vfx:"|&vfx;[&loop(after,:)]
  --%|
  --?"[&sfx]" -ne ""|[
    --a|[&sfx]
  --]|
  --?"[&vfx]" -ne ""|[
    --&vfxAt|[&attack_target]
    --?"[&vfx]" -inc "s "|&vfxAt;[*S:t-id]
    --vtoken|[&vfxAt] [&vfx(after, )]
  --]|
  --&attack_crit|0
  --&rollType|1d20
  --?"[*S:advantagetoggle]" -inc advantage|&rollType;2d20kh1
  --?"[*S:advantagetoggle]" -inc disadvantage|&rollType;2d20kl1
  --?[&selfHandleAdvantageMode] -eq 1 -and [&currentAdvantageMode] -eq 0|&rollType;1d20
  --?[&selfHandleAdvantageMode] -eq 1 -and [&currentAdvantageMode] -eq 1|&rollType;2d20kh1
  --?[&selfHandleAdvantageMode] -eq 1 -and [&currentAdvantageMode] -eq 2|&rollType;2d20kl1  
  --?"[&TNPC_ATTACKAGAINSTMODE]" -eq ""|SKIP_PC_ATTACK_NPC_ROLL_MODE_CALC
  --?"[&rollType]" -eq "1d20" -and "[&TNPC_ROLLMODE]" -eq "1d20"|&rollType;1d20
  --?"[&rollType]" -eq "2d20kh1" -and "[&TNPC_ROLLMODE]" -eq "1d20"|&rollType;2d20kh1
  --?"[&rollType]" -eq "2d20kl1" -and "[&TNPC_ROLLMODE]" -eq "1d20"|&rollType;2d20kl1
  --?"[&rollType]" -eq "2d20kh1" -and "[&TNPC_ROLLMODE]" -eq "2d20kh1"|&rollType;2d20kh1
  --?"[&rollType]" -eq "2d20kl1" -and "[&TNPC_ROLLMODE]" -eq "2d20kl1"|&rollType;2d20kl1
  --?"[&rollType]" -eq "2d20kh1" -and "[&TNPC_ROLLMODE]" -eq "2d20kl1"|&rollType;1d20
  --?"[&rollType]" -eq "2d20kl1" -and "[&TNPC_ROLLMODE]" -eq "2d20kh1"|&rollType;1d20
  --:SKIP_PC_ATTACK_NPC_ROLL_MODE_CALC|
  --&arString|[&rollType]
  --=attrMod|0
  --&attrName|Attr
  -->Get_Ability_Modifier|[*R:atkattr_base];attrMod;attrName
  --/|Assume the damage attribute is the same unless it is set to something else
  --=dmgMod|[$attrMod.Raw]
  --&dmgAttrName|[&attrName]
  -->Get_Ability_Modifier|[*R:dmgattr];dmgMod;dmgAttrName
  --/|Assume the damage attribute is the same unless it is set to something else
  --=dmg2Mod|[$attrMod.Raw]
  --&dmg2AttrName|[&attrName]
  -->Get_Ability_Modifier|[*R:dmg2attr];dmg2Mod;dmg2AttrName  
  --?[$attrMod.Raw] -gt 0|&arString;+ + [$attrMod.Raw] [&attrName]
  --?[$attrMod.Raw] -lt 0|&arString;+ [$attrMod.Raw] [&attrName]
  --?[*R:atkmod] -gt 0|&arString;+ + [*R:atkmod] [Special]
  --?[*R:atkmod] -lt 0|&arString;+ [*R:atkmod] [Special]
  --?[*R:atkprofflag] -ne 0|&arString;+ + [*S:pb] [Prof]
  --?"[*R:atkprofflag]" -eq ""|&arString;+ + [*S:pb] [Prof]
  --?[*R:atkmagic] -gt 0|&arString;+ + [*R:atkmagic] [Magic]
  --?[*R:atkmagic] -lt 0|&arString;+ [*R:atkmagic] [Magic]
  --=attackRoll|[&arString] [&PC_GAM]
  --&dmgroll|[*R:dmgbase]
  --?[$dmgMod.Raw] -gt 0|&dmgroll;+ + [$dmgMod.Raw] [&dmgAttrName]
  --?[$dmgMod.Raw] -lt 0|&dmgroll;+ [$dmgMod.Raw] [&dmgAttrName]
  --?[*R:dmgmod] -gt 0|&dmgroll;+ + [*R:dmgmod] [Special]
  --?[*R:dmgmod] -lt 0|&dmgroll;+ [*R:dmgmod] [Special]
  --?[*R:atkmagic] -gt 0|&dmgroll;+ + [*R:atkmagic] [Magic]
  --?[*R:atkmagic] -lt 0|&dmgroll;+ [*R:atkmagic] [Magic]
  --&dmgroll2|[*R:dmg2base]
  --?[*R:dmg2attr] -gt 0|&dmgroll2;+ + [$dmg2Mod.Raw] [&dmg2AttrName]
  --?[*R:dmg2attr] -lt 0|&dmgroll2;+ [$dmg2Mod.Raw] [&dmg2AttrName]
  --?[*R:dmg2mod] -gt 0|&dmgroll;+ + [*R:dmg2mod] [Special]
  --?[*R:dmg2mod] -lt 0|&dmgroll;+ [*R:dmg2mod] [Special]
  --/?[*R:atkmagic] -gt 0|&dmgroll2;+ + [*R:atkmagic] [Magic]
  --/?[*R:atkmagic] -lt 0|&dmgroll2;+ [*R:atkmagic] [Magic]
  --=AC|[*T:ac]
  --?"[*T:npc_ac]" -ne ""|=AC;[*T:npc_ac] [&NPC_ACMOD]
  --?[&sendGmInfo] -eq 1|[
    --+[*R:atkname] Attack|rolled [$attackRoll] vs the target's AC[br][br]
    --*[*R:atkname] Attack|rolled [$attackRoll] vs AC [b][$AC.Raw][/b]
  --]|[
    --+[*R:atkname] Attack|rolled [$attackRoll] vs AC [b][$AC.Raw][/b][br][br]
  --]|
  --=CritNum|20
  --?"[*S:default_critical_range]" -ne "" -and "[*S:default_critical_range]" -ne "undefined"|=CritNum;[*S:default_critical_range]
  --?"[*R:atkcritrange]" -ne "" -and "[*R:atkcritrange]" -ne "undefined"|=CritNum;[*R:atkcritrange] {MAX:[$CritNum.Raw]}
  --&critRoll|[*R:dmgbase]
  --&critRoll2|[*R:dmg2base]
  --?"[*R:dmgcustcrit]" -eq ""|DONE_ATK_CRIT
  --&critRoll|[*R:dmgcustcrit]
  --:DONE_ATK_CRIT|
  --?"[*R:dmg2custcrit]" -eq ""|DONE_ATK_CRIT2
  --&critRoll2|[*R:dmg2custcrit]
  --:DONE_ATK_CRIT2|
  --?[$attackRoll.Base] -ge [$CritNum.Raw]|=attack_crit;1|=attack_crit;0
  --?[$attack_crit] -eq 1|&dmgroll;+ + [&critRoll] [Crit]
  --?[$attack_crit] -eq 1|&dmgroll2;+ + [&critRoll2] [Crit]
  --?[$attack_crit] -eq 1|&dmgroll;+ [&PC_GDM_CRIT]|&dmgroll;+ [&PC_GDM]
  --?[$attackRoll.Total] -lt [$AC]|ATTACK_MISS
  --?[$attackRoll.Base] -eq 1|ATTACK_MISS
  --?[$attack_crit] -eq 1|&hittag;Critical Hit!|&hittag;Hit!
  --?[$attack_crit] -eq 1|&hitdesc;critically hits|&hitdesc;hits
  --&dmgModType1|
  --&dmgModType2|
  --&isMagical|0
  --?"[*R:atkmagic]" -ne "" -and "[*R:atkmagic]" -ne "0"|&isMagical;1
  --&isSilver|0
  --?"[*R:atkname]" -inc "Silver"|&isSilver;1
  --&isAdamant|0
  --?"[*R:atkname]" -inc "Adamantine"|&isAdamant;1
  --/+|Magic: [&isMagical], Silver: [&isSilver], Ad: [&isAdamant]
  --?"[*T:npc]" -ne "1" -or [&UseResistances] -eq 0|SKIP_CALC_DMG_MODS
  -->CheckDamageModifiersNPC|[*T:character_id];dmgModType1;[*R:dmgtype];[&isMagical];[&isSilver];[&isAdamant];PC_GDM_RES
  -->CheckDamageModifiersNPC|[*T:character_id];dmgModType2;[*R:dmg2type];[&isMagical];[&isSilver];[&isAdamant]
  --~gdmlist|array;fromstring;GDM_RES;|;[&PC_GDM_RES]
  --%gdmloop|foreach;GDM_RES
    --\|[&gdmloop]
  --%|
  --:SKIP_CALC_DMG_MODS|
  --=attackDamage|[&dmgroll] [&dmgModType1]
  --=attackDamage2|[&dmgroll2] [&dmgModType2]
  --?"[*R:dmg2flag]" -eq "0"|ATTACK_ONE_DAMAGE
  --?"[*R:dmg2flag]" -eq ""|ATTACK_ONE_DAMAGE
  --^ATTACK_TWO_DAMAGE|

  --:ATTACK_ONE_DAMAGE|
  --+[&hittag]|The [*R:atkname] [&hitdesc] and deals [$attackDamage] [*R:dmgtype] damage.[br][br]
  -->[&damageRoutine]|[&attack_target];[&hitPointsBar];-[$attackDamage.Raw]
  --^ATTACK_DONE|

  --:ATTACK_TWO_DAMAGE|
  --+[&hittag]|The [*R:atkname] [&hitdesc] and deals [$attackDamage] [*R:dmgtype] and [$attackDamage2] [*R:dmg2type] damage.[br][br]
  --=TotalDamage|[$attackDamage.Raw] + [$attackDamage2.Raw]
  -->[&damageRoutine]|[&attack_target];[&hitPointsBar];-[$TotalDamage.Raw]
  --^ATTACK_DONE|

  --:ATTACK_MISS|
  --+The [*R:atkname] attack missed!|[br][br]

  --:ATTACK_DONE|
  --?[&requireAmmo] -eq 0|NO_AMMO_USED
  --?"[&AmmoAttrName]" -eq ""|NO_AMMO_USED
  --!a:[*S:character_id]|[&AmmoAttributeID]:-=1

  --:NO_AMMO_USED|
  
  --?"[&PCPostAttackFunction]" -ne ""|>[&PCPostAttackFunction]
  --X|

  --:EXEC_PC_CANTRIP|
  --?[&AMOriginalSendingPlayer] -ne [&SendingPlayerID]|YOU_CANNOT_DO_THAT
  --/+|[hr #58180d]
  --&whisperTarget|
  --?[*S:npc] -eq 1 -and [&AllNPCActionsToSender] -eq 1||&whisperTarget;self
  --#whisper|[&whisperTarget]

  --~cantrip_name|string;before;|;[&reentryval]
  --~cantrip_target|string;after;|;[&reentryval]
  --#whisper|
  --#emotestate|visible
  --#targettoken|[&cantrip_target]
  --/\|Emote in PC Cantrip|[*S:t-name] uses [&attack_name] against [*T:t-name]
  --#emotetext|[*S:t-name] uses [&cantrip_name] against [*T:t-name]
  -->Find_Active_Global_Attack_Modifiers|[*S:character_id];PC_GAM
  -->Find_Active_Global_Damage_Modifiers|[*S:character_id];PC_GDM
  -->Find_Active_Global_Damage_Modifiers|[*S:character_id];PC_GDM_CRIT;1
  -->Find_Active_Global_Save_Modifiers|[$cantrip_target];TARGET_GSM
  --Rfind|[*S:character_id];[&cantrip_name];repeating_spell-cantrip;spellname
  --?"[*R:spelloutput]" -eq ATTACK|PC_ATTACK_CANTRIP|PC_CARD_CANTRIP
  --=cantrip_crit|0
  --:PC_ATTACK_CANTRIP|
  --?"[*R:spellattack]" -eq "Ranged" -or "[*R:spellattack]" -eq "Melee"|CANTRIP_ATTACK_ROLL|CANTRIP_DONE_ATTACK_ROLL
  --:CANTRIP_ATTACK_ROLL|
  --&rollType|1d20
  --?"[*S:advantagetoggle]" -inc advantage|&rollType;2d20kh1
  --?"[*S:advantagetoggle]" -inc disadvantage|&rollType;2d20kl1
  --?[&selfHandleAdvantageMode] -eq 1 -and [&currentAdvantageMode] -eq 0|&rollType;1d20
  --?[&selfHandleAdvantageMode] -eq 1 -and [&currentAdvantageMode] -eq 1|&rollType;2d20kh1
  --?[&selfHandleAdvantageMode] -eq 1 -and [&currentAdvantageMode] -eq 2|&rollType;2d20kl1  
  --=attackRoll|[&rollType] + [*S:spell_attack_bonus] [Spell Atk Bonus] [&PC_GAM]
  --=AC|[*T:ac]
  --?"[*T:npc_ac]" -ne ""|=AC;[*T:npc_ac]
  --?[&sendGmInfo] -eq 1|[
    --+[*R:spellattack] Attack|rolled [$attackRoll] vs the target's AC[br][br]
    --*[*R:spellattack] Attack|rolled [$attackRoll] vs AC [b][$AC.Raw][/b]
  --]|[
    --+[*R:spellattack] Attack|rolled [$attackRoll] vs AC [b][$AC.Raw][/b][br][br]
  --]|
  --?[$attackRoll.Base] -ge 20|=cantrip_crit;1|=cantrip_crit;0
  --?[$attackRoll.Total] -lt [$AC]|CANTRIP_ATTACK_MISS
  --?[$attackRoll.Base] -eq 1|CANTRIP_ATTACK_MISS
  --:CANTRIP_DONE_ATTACK_ROLL|
  --&damageRoll|[*R:spelldamage]
  --~nonScale|array;indexof;NonScalingCantrips;[&cantrip_name]
  --?[&nonScale] -ne "ArrayError"|SKIP_CANTRIP_SCALE
  --?[*S:level] -ge 5|&damageRoll;+ + [*R:spelldamage]
  --?[*S:level] -ge 11|&damageRoll;+ + [*R:spelldamage]
  --?[*S:level] -ge 17|&damageRoll;+ + [*R:spelldamage]
  --:SKIP_CANTRIP_SCALE|
  --?[$cantrip_crit] -eq 1|&damageRoll;+ + [&damageRoll]
  --?[$cantrip_crit] -eq 1|&damageRoll;+ [&PC_GDM_CRIT]|&damageRoll;+ [&PC_GDM]
  --&dmgModType1|
  --?"[*T:npc]" -ne "1" -or [&UseResistances] -eq 0|SKIP_PC_CANTRIP_RESIST
  -->CheckDamageModifiersNPC|[*T:character_id];dmgModType1;[*R:spelldamagetype];1;0;0
  --:SKIP_PC_CANTRIP_RESIST|
  --=cantripDamage|[&damageRoll] [&dmgModType1]
  --?"[*R:spellsave]" -ne ""|CANTRIP_SAVE|CANTRIP_NO_SAVE
  --:CANTRIP_NO_SAVE|
  --?[$cantrip_crit] -eq 1|&hittag;Critical Hit!|&hittag;Hit!
  --?[$cantrip_crit] -eq 1|&hitdesc;critically hits|&hitdesc;hits
  --+[&hittag]|The [*R:spellname] [&hitdesc] and deals [$cantripDamage] [*R:spelldamagetype] damage.[br][br]
  -->[&damageRoutine]|[&cantrip_target];[&hitPointsBar];-[$cantripDamage.Raw]
  --^PC_CARD_CANTRIP|
  --:CANTRIP_SAVE|
  --+|The target needs to make a DC [*S:spell_save_dc] [*R:spellsave] saving throw or take [$cantripDamage] [*R:spelldamagetype] damage.[br][br]
  --/&output|The [b][i][*R:spellname][/i][/b] deals [$cantripDamage] [*R:spelldamagetype] damage. 
  --/?"[*R:spellsave]" -ne ""|&output;+ The target can make a [*R:spellsave] save vs DC [*S:spell_save_dc] to negate the damage.
  --^PC_CARD_CANTRIP|
  --:CANTRIP_ATTACK_MISS|
  --+The [*R:spellname] attack missed!|[br][br]
  --:PC_CARD_CANTRIP|
  --#title|[&cantrip_name]
  --#leftsub|[*S:character_name]
  --#rightsub|
  --?[&nonScale] -eq "ArrayError"|SKIP_CANTRIP_REPEAT
  --#rightsub|[rbutton][&RepeatImage]::EXEC_PC_CANTRIP;[&cantrip_name]|[&TB]target|token_id}[/rbutton]
  --:SKIP_CANTRIP_REPEAT|
  --+|[*R:spelldescription]
  --:PC_DONE_CANTRIP|
  --\call|Calling PostCantrip
  --?"[&PCPostCantripCastFunction]" -ne ""|>[&PCPostCantripCastFunction]
  --\done|Done PostCantrip
  --X|

  --:EXEC_PC_SPELL|spellInfo1=Spell Level, 2=SpellName, 3=Target ID, 4=Slot Level
  --/+|[hr #58180d]
  --&whisperTarget|
  --?[*S:npc] -eq 1 -and [&AllNPCActionsToSender] -eq 1||&whisperTarget;self
  --#whisper|[&whisperTarget]
  --~spellInfo|string;split;|;[&reentryval]

  --=UpCast|[&spellInfo4] - [&spellInfo1]
  --?"[&UseAMAShadowdarkSystem]" -eq "1"|DO_SHADOWDARK_SPELL_CHECK
  --?[&trackPCSpellSlots] -eq 0|SKIP_CASTING_SLOT_CHECK
  --?"[&spellInfo4]" -eq "As Ritual"|SKIP_CASTING_SLOT_CHECK
  --?"[&UseAMAShadowdarkSystem]" -ne "1"|CAST_SKIP_SHADOWDARK

  --:DO_SHADOWDARK_SPELL_CHECK|
  --/|Suport for optional Shadowdark system
  --Rfind|[*S:character_id];[&spellInfo2];repeating_spell-[&spellInfo1];spellname
  --=SDCastMod|[*S:level] \ 2 {MAX:5} + [*S:intelligence_mod]
  -->_LibAMA_SHADOWDARK_SPELLCAST_CHECK|[*R:spellprepared];[&spellInfo1];[*S:character_id];SD_Result;[*R>innate];[$SDCastMod.Raw]
  --?[&SD_Result] -eq 0|CAST_SKIP_SHADOWDARK
  --#whisper|
  --^PC_DONE_SPELL|
  --:CAST_SKIP_SHADOWDARK|
  
  -->GET_AVAILABLE_SPELL_SLOTS|[&spellInfo4];castSlotAvailable
  --?[&castSlotAvailable] -eq 0|NO_SLOTS_AVAILABLE
  -->DEDUCT_SPELL_SLOT|[&spellInfo4]
  --^SKIP_CASTING_SLOT_CHECK|
  --:NO_SLOTS_AVAILABLE|
  --#whisper|self
  --+|You do not have a level [$spellInfo4] spell slot available to cast [&spellInfo2]
  --^PC_DONE_SPELL|
  --:SKIP_CASTING_SLOT_CHECK|
  --&action_target|[&spellInfo3]
  --#title|[&spellInfo2]
  --#leftsub|[*S:character_name]
  --#rightsub|
  --#whisper|
  --#emotestate|visible
  --#targettoken|[&action_target]
  --/\|Emote in PC Spell|[*S:t-name] uses [&attack_name] against [*T:t-name]
  --#emotetext|[*S:t-name] uses [&spellInfo2] against [*T:t-name]
  -->Find_Active_Global_Attack_Modifiers|[*S:character_id];PC_GAM
  -->Find_Active_Global_Damage_Modifiers|[*S:character_id];PC_GDM
  -->Find_Active_Global_Damage_Modifiers|[*S:character_id];PC_GDM_CRIT;1
  -->Find_Active_Global_Damage_Modifiers|[*S:character_id];PC_GDM_RES
  -->Find_Active_Global_Damage_Modifiers|[*S:character_id];PC_GDM_RES_CRIT;1
  -->Find_Active_Global_Save_Modifiers|[$cantrip_target];TARGET_GSM
  --Rfind|[*S:character_id];[&spellInfo2];repeating_spell-[&spellInfo1];spellname
  --#targettoken|[&spellInfo3]
  --?"[*R:spellconcentration]" -inc "concentration=1" -and "[&ConcentrationMarkerName]" -ne "0"|>ADD_STATUS_MARKER;[&saveTokenID];[&ConcentrationMarkerName]
  --?"[*R:spelloutput]" -eq ATTACK|PC_ATTACK_SPELL|PC_CARD_SPELL

  --:PC_ATTACK_SPELL|
  --&spell_crit|0
  --?"[*R:spellattack]" -eq "Ranged" -or "[*R:spellattack]" -eq "Melee"|SPELL_ATTACK_ROLL|SPELL_DONE_ATTACK_ROLL

  --:SPELL_ATTACK_ROLL|
  --&rollType|1d20
  --?"[*S:advantagetoggle]" -inc advantage|&rollType;2d20kh1
  --?"[*S:advantagetoggle]" -inc disadvantage|&rollType;2d20kl1
  --?[&selfHandleAdvantageMode] -eq 1 -and [&currentAdvantageMode] -eq 0|&rollType;1d20
  --?[&selfHandleAdvantageMode] -eq 1 -and [&currentAdvantageMode] -eq 1|&rollType;2d20kh1
  --?[&selfHandleAdvantageMode] -eq 1 -and [&currentAdvantageMode] -eq 2|&rollType;2d20kl1  
  --=attackRoll|[&rollType] + [*S:spell_attack_bonus] [Spell Atk Bonus] [&PC_GAM]
  --=AC|[*T:ac]
  --?"[*T:npc_ac]" -ne ""|=AC;[*T:npc_ac]
    --?[&sendGmInfo] -eq 1|[
    --+[*R:spellattack] Attack|rolled [$attackRoll] vs the target's AC[br][br]
    --*[*R:spellattack] Attack|rolled [$attackRoll] vs AC [b][$AC.Raw][/b]
  --]|[
    --+[*R:spellattack] Attack|rolled [$attackRoll] vs AC [b][$AC.Raw][/b][br][br]
  --]|
  --?[$attackRoll.Base] -ge 20|=spell_crit;1|=spell_crit;0
  --?[$attackRoll.Total] -lt [$AC]|SPELL_ATTACK_MISS

  --:SPELL_DONE_ATTACK_ROLL|
  --?"[*T:npc]" -ne "1" -or [&UseResistances] -eq 0|SKIP_PC_SPELL_RESIST
  --&dmgModType1|
  --&dmgModType2|
  -->CheckDamageModifiersNPC|[*T:character_id];dmgModType1;[*R:spelldamagetype];1;0;0
  -->CheckDamageModifiersNPC|[*T:character_id];dmgModType2;[*R:spelldamagetype2];1;0;0
  --~gdmlist|array;fromstring;GDM_RES;|;[&PC_GDM_RES]
  --%gdmloop|foreach;GDM_RES
    --\|[&gdmloop]
  --%|
  --:SKIP_PC_SPELL_RESIST|
  
  --&damageRoll|[*R:spelldamage]
  --?[$spell_crit] -eq 1|&damageRoll;+ [&PC_GDM_CRIT]|&damageRoll;+ [&PC_GDM]
  --&damageRoll2|[*R:spelldamage2]

  --&ability_bonus|
    --?"[*R:spell_ability]" -eq "spell"|[
      --~ability|string;before;};[*S:spellcasting_ability]
      --~ability|string;after;{;[&ability]
      --?[&ability] -eq strength_mod|&ability_bonus; + [*S:strength_mod] [Spellcasting Ability]
      --?[&ability] -eq dexterity_mod|&ability_bonus; + [*S:dexterity_mod] [Spellcasting Ability]
      --?[&ability] -eq constitution_mod|&ability_bonus; + [*S:constitution_mod] [Spellcasting Ability]
      --?[&ability] -eq intelligence_mod|&ability_bonus; + [*S:intelligence_mod] [Spellcasting Ability]
      --?[&ability] -eq wisdom_mod|&ability_bonus; + [*S:wisdom_mod] [Spellcasting Ability]
      --?[&ability] -eq charisma_mod|&ability_bonus; + [*S:charisma_mod] [Spellcasting Ability]
  --]|
  --&spell_heal|0
  --?"[*R:spellhealing]" -ne ""|&spell_heal;1
  --?"[*R:spellhealing]" -ne ""|&damageRoll;[*R:spellhealing]
  --&damageToApply|-
  --?[$UpCast] -eq 0|NO_UPCAST
  --=upcastDice|[$UpCast.Raw] * [*R:spellhldie]
  --&damageRoll|+ + [$upcastDice.Raw][*R:spellhldietype] [Upcasting]

  --:NO_UPCAST|
  --?[$spell_crit] -eq 1|&damageRoll;+ + [&damageRoll] 
  --?[$spell_crit] -eq 1|&damageRoll2;+ + [&damageRoll2] 
  --?[$spell_crit] -eq 1|&hittag;Critical Hit!|&hittag;Hit!
  --?[$spell_crit] -eq 1|&hitdesc;critically hits|&hitdesc;hits and deals
  --?[&spell_heal] -eq 1|&hittag;[b][/b]  
  --?[&spell_heal] -eq 1|&hitdesc;heals for
  --?[&spell_heal] -eq 1|[
    --&damageToApply|
  --]|
 
  --=spellDamage|[&damageRoll] [&dmgModType1]
  --=spellDamage2|[&damageRoll2] [&dmgModType2]
  --=TotalDamage|[$spellDamage.Raw] + [$spellDamage2.Raw]

  --?"[*R:spelldamage2]" -ne ""|TWO_DAMAGE
  --+[&hittag]|The [*R:spellname] [&hitdesc] [$spellDamage] [*R:spelldamagetype] damage.[br][br]
  --&damageToApply|+[$spellDamage.Raw]
  -->[&damageRoutine]|[&action_target];[&hitPointsBar];[&damageToApply]
  --^DONE_SPELL_DAMAGE|

  --:TWO_DAMAGE|
  --+[&hittag]|The [*R:spellname] [&hitdesc] [$spellDamage] [*R:spelldamagetype] damage and [$spellDamage2] [*R:spelldamagetype2] damage.[br][br]
  --&damageToApply|+[$TotalDamage.Raw]
  -->[&damageRoutine]|[&action_target];[&hitPointsBar];[&damageToApply]

  --:DONE_SPELL_DAMAGE|
  --?"[*R:spellsave]" -eq ""|PC_CARD_SPELL
  --+|The target can make a [*R:spellsave] save vs DC [*S:spell_save_dc] to modify the damage as described below.[br][br]
  --^PC_CARD_SPELL|

  --:SPELL_ATTACK_MISS|
  --+The [*R:spellname] attack missed!|[br][br]

  --:PC_CARD_SPELL|
  
  --+|[*R:spelldescription]

  --:PC_DONE_SPELL|
    --\|calling post
    --?"[&PCPostSpellCastFunction]" -ne ""|>[&PCPostSpellCastFunction]
    --\|done post
  --X|

  --:SAVING_THROW|
  --&whisperTarget|
  --?[*S:npc] -eq 1 -and [&AllNPCActionsToSender] -eq 1||&whisperTarget;self
  --#whisper|[&whisperTarget]
  -->Find_Active_Global_Save_Modifiers|[*S:character_id];GSM_VALUES
  --~saveInfo|string;split;|;[&reentryval]
  --~saveType|string;tolowercase;[&saveInfo1]_save_bonus
  --&saveBonus|[*S:[&saveType]]
  --&rollType|[&saveInfo2]
  --&rollEquation|1d20
  --?[&rollType] -eq "Advantage"|&rollEquation;2d20kh1
  --?[&rollType] -eq "Disadvantage"|&rollEquation;2d20kl1
  -->Get_Ability_Modifier|[&saveInfo1(tolowercase)]_mod;attrMod;attrName
  --=SavingThrow|[&rollEquation] + [&saveBonus] [&attrName] [&GSM_VALUES]
  --#title|[&saveInfo1] Save
  --#leftsub|[*S:character_name]
  --#rightsub|
  --+|Saving throw ([&attrName]) result: [$SavingThrow]
  --X|

  --:SKILL_CHECK|
  --&whisperTarget|
  --?[*S:npc] -eq 1 -and [&AllNPCActionsToSender] -eq 1||&whisperTarget;self
  --#whisper|[&whisperTarget]
  --~skillInfo|string;split;|;[&reentryval]
  --~skillType|string;tolowercase;[&skillInfo1]_bonus
  --&rollType|[&skillInfo2]
  --&skillDisplayName|[&skillInfo1]
  --?[&skillDisplayName] -eq "animal_handling"|&skillDisplayName;Animal Handling
  --?[&skillDisplayName] -eq "sleight_of_hand"|&skillDisplayName;Sleight of Hand
  --&rollEquation|1d20
  --?[&rollType] -eq "Advantage"|&rollEquation;2d20kh1
  --?[&rollType] -eq "Disadvantage"|&rollEquation;2d20kl1
  --=SkillCheck|[&rollEquation] + [*S:[&skillType]] [&obrac][&skillInfo1][&cbrac]
  --#title|[&skillDisplayName] Check
  --#leftsub|[*S:character_name]
  --#rightsub|
  --+|[&skillDisplayName] check result: [$SkillCheck]
  --X|

  --:TOOL_CHECK|
  --&whisperTarget|
  --?[*S:npc] -eq 1 -and [&AllNPCActionsToSender] -eq 1||&whisperTarget;self
  --#whisper|[&whisperTarget]
  --~toolInfo|string;split;|;[&reentryval]
  --&tool_base_roll|1d20
  --?[&toolInfo2] e-eq "Advantage"|&tool_base_roll;2d20kh1
  --?[&toolInfo2] e-eq "Disadvantage"|&tool_base_roll;2d20kl1
  --rfind|[*S:character_id];[&toolInfo1];repeating_tool;toolname
  --?"[*R:toolname]" -eq "NoRepeatingAttributeLoaded"|BAD_TOOL_ROLL
  --&toolattr|[*R:toolattr]_mod
  --&toolattr|[&toolattr(tolowercase)]
  --&toolbonus|[*R:toolbonus_base]
  --&toolproftype|[PROF]
  --?"[&toolbonus]" -inc "(pb*2)|&toolproftype;[EXPERT]
  --?"[&toolbonus]" -inc "(pb/2)|&toolproftype;[JACK]
    --~toolbonus|string;replace;(pb*2);[*S:pb] * 2;[&toolbonus]
  --~toolbonus|string;replace;(floor(pb/2));[*S:pb] \ 2;[&toolbonus]
  --?"[&toolbonus]" -eq ""|&toolbonus;[*S:pb]
  --=toolprofbonus|[&toolbonus]
  --?"[&toolattr]" -eq "query_mod"|TOOL_ATTR_QUERY
  --:DO_TOOL_ROLL|
  --&toolattr_display|[
  --&toolattr_display|+[&toolattr(0,3)]
  --&toolattr_display|+]
  --&toolRoll|[&tool_base_roll] + [*S:[&toolattr]] [&toolattr_display(touppercase)] + [$toolprofbonus.Raw] [&toolproftype]
  --=ToolRollResult|[&toolRoll]
  --+[&toolInfo1] Result|[$ToolRollResult]
  --X|
  --:BAD_TOOL_ROLL|
   --+|Bad tool info : [&toolInfo1]
  --X|
  --:TOOL_ATTR_QUERY|
  --#whisper|self
  --+[&toolInfo1]|This tool requires you to select the associated attribute to roll against. Please click the button below to choose.
  --+|[c][rbutton]Choose Attribute::TOOL_ATTR_RETURN;[&QB]Skill to roll?|Strength,strength_mod|Dexterity,dexterity_mod|Constitution,constitution_mod|Intelligence,intelligence_mod|Wisdom,wisdom_mod|Charisma,charisma_mod[&QE][/rbutton][/c]
  --X|
  --:TOOL_ATTR_RETURN|
  --~toolAttrInfo|string;split;|;[&reentryval]
  --&toolattr|[&toolAttrInfo1]
  --&whisperTarget|
  --?[*S:npc] -eq 1 -and [&AllNPCActionsToSender] -eq 1||&whisperTarget;self
  --#whisper|[&whisperTarget]
  --^DO_TOOL_ROLL|
  --?"[*R:toolname]" -eq "NoRepeatingAttributeLoaded"|BAD_TOOL_ROLL
  --X|

  --/|=========================================================================================================
  --/| FUNCTIONS

  --/|The three Find_Active_Global_X_Modifiers functions will loop through the various global modifiers repeating
  --/|sections and construct a line containing the dice roll and description (in square brackets) for each entry
  --/|that has an active flag set. This is set to a variable named after the second parameter and can be appended
  --/|directly to an attack/damage/save roll

  --:SET_ADV_MODE|
  --&currentAdvantageMode|[&reentryval]
  --^AFTER_ADVMODE_CHANGE|

  --:Find_Active_Global_Attack_Modifiers|character_id;StringVariableToSet
  --&_GAM_Temp| 
  --Rfirst|[%1%];repeating_tohitmod
  --:_GAM_Loop|
  --?"[*R:global_attack_active_flag]" -eq "1"|>_AddGAM;[*R:global_attack_name];[*R:global_attack_roll]
  --Rnext| 
  --?"[*R:global_attack_name]" -ne "NoRepeatingAttributeLoaded"|_GAM_Loop
  --&[%2%]|[&_GAM_Temp] 
  --<|
  
  --:_AddGAM| 
  --&_GAM_Temp|+ + [%2%]# [&obrac][%1%][&cbrac] 
  --<|

  --:Find_Active_Global_Damage_Modifiers|character_id;StringVariableToSet;isCrit(1=true);StringVar2forResistances
  --&_GDM_Temp| 
  --&_GDM_Temp2|
  --Rfirst|[%1%];repeating_damagemod
  --:_GDM_Loop|
  --?"[*R:global_damage_active_flag]" -eq "1"|>_AddGDM;[*R:global_damage_name];[*R:global_damage_damage];[%3%];[*R:global_damage_critical_damage];[*R:global_damage_type]
  --Rnext| 
  --?"[*R:global_damage_name]" -ne "NoRepeatingAttributeLoaded"|_GDM_Loop
  --&[%2%]|[&_GDM_Temp] 
  --&[%4%]|[&_GDM_Temp2] 
  --<|
  
  --:_AddGDM| 
  --&_GDM_Temp|+ + [%2%]# [%5%] damage [&obrac][%1%][&cbrac] 
  --?"[%3%]" -eq "1"|&_GDM_Temp;+ + [%4%]  [ CRIT [%1%] ]
  --&_GDM_Temp2|+[%2%]# [%5%] damage [&obrac][%1%][&cbrac][&vbar]
  --<|

  --:Find_Active_Global_Save_Modifiers|character_id;StringVariableToSet
  --&_GSM_Temp| 
  --Rfirst|[%1%];repeating_savemod
  --:_GSM_Loop|
  --?"[*R:global_save_active_flag]" -eq "1"|>_AddGSM;[*R:global_save_name];[*R:global_save_roll]
  --Rnext| 
  --?"[*R:global_save_name]" -ne "NoRepeatingAttributeLoaded"|_GSM_Loop
  --&[%2%]|[&_GSM_Temp] 
  --<|

    --:_AddGSM| 
  --&_GSM_Temp|+ + [%2%]# [&obrac][%1%][&cbrac] 
  --<|

  --:Create_Global_Attack_Modifier_Buttons|character_id;StringVariableToSet
  --&_GAMB_Temp| 
  --Rfirst|[%1%];repeating_tohitmod
  --:_GAMB_Loop|
  --?"[*R:global_attack_name]" -eq "NoRepeatingAttributeLoaded"|_GAMB_DONE
  --?"[*R:global_attack_active_flag]" -eq "1"|>_AddGAMB;[*R:global_attack_name];[&GAM_Button_Active_Color]
  --?"[*R:global_attack_active_flag]" -ne "1"|>_AddGAMB;[*R:global_attack_name];[&GAM_Button_Inactive_Color]
  --Rnext| 
  --?"[*R:global_attack_name]" -ne "NoRepeatingAttributeLoaded"|_GAMB_Loop
  --:_GAMB_DONE|
  --&[%2%]|[&_GAMB_Temp] 
  --<|
  
  --:_AddGAMB| 
  --&_GAMB_Temp|+[rbutton:#ffffff:#[%2%]:8px][%1%]::TOGGLE_GAM;[%1%][/rbutton][&SPACE]
  --<|

  --:Create_Global_Damage_Modifier_Buttons|character_id;StringVariableToSet
  --&_GDMB_Temp| 
  --Rfirst|[%1%];repeating_damagemod
  --:_GDMB_Loop|
  --?"[*R:global_damage_name]" -eq "NoRepeatingAttributeLoaded"|_GDMB_DONE
  --?"[*R:global_damage_active_flag]" -eq "1"|>_AddGDMB;[*R:global_damage_name];[&GAM_Button_Active_Color]
  --?"[*R:global_damage_active_flag]" -ne "1"|>_AddGDMB;[*R:global_damage_name];[&GAM_Button_Inactive_Color]  
  --Rnext| 
  --?"[*R:global_damage_name]" -ne "NoRepeatingAttributeLoaded"|_GDMB_Loop
  --:_GDMB_DONE|
  --&[%2%]|[&_GDMB_Temp] 
  --<|
  
  --:_AddGDMB| 
  --&_GDMB_Temp|+[rbutton:#ffffff:#[%2%]:8px][%1%]::TOGGLE_GDM;[%1%][/rbutton][&SPACE]
  --<|

  --:Create_Global_Save_Modifier_Buttons|character_id;StringVariableToSet
  --&_GSMB_Temp| 
  --Rfirst|[%1%];repeating_savemod
  --:_GSMB_Loop|
  --?"[*R:global_save_name]" -eq "NoRepeatingAttributeLoaded"|_GSMB_DONE
  --?"[*R:global_save_active_flag]" -eq "1"|>_AddGSMB;[*R:global_save_name];[&GAM_Button_Active_Color]
  --?"[*R:global_save_active_flag]" -ne "1"|>_AddGSMB;[*R:global_save_name];[&GAM_Button_Inactive_Color]  
  --Rnext| 
  --?"[*R:global_save_name]" -ne "NoRepeatingAttributeLoaded"|_GSMB_Loop
  --:_GSMB_DONE|
  --&[%2%]|[&_GSMB_Temp] 
  --<|
  
  --:Create_Global_AC_Modifier_Buttons|character_id;StringVariableToSet
  --&_GACMB_Temp| 
  --Rfirst|[%1%];repeating_acmod
  --:_GACMB_Loop|
  --?"[*R:global_ac_name]" -eq "NoRepeatingAttributeLoaded"|_GACMB_DONE
  --?"[*R:global_ac_active_flag]" -eq "1"|>_AddGACMB;[*R:global_ac_name];[&GAM_Button_Active_Color]
  --?"[*R:global_ac_active_flag]" -ne "1"|>_AddGACMB;[*R:global_ac_name];[&GAM_Button_Inactive_Color]  
  --Rnext| 
  --?"[*R:global_ac_name]" -ne "NoRepeatingAttributeLoaded"|_GACMB_Loop
  --:_GACMB_DONE|
  --&[%2%]|[&_GACMB_Temp] 
  --<|

  --:Create_Global_Skill_Modifier_Buttons|character_id;StringVariableToSet
  --&_GSKMB_Temp| 
  --Rfirst|[%1%];repeating_skillmod
  --:_GSKMB_Loop|
  --?"[*R:global_skill_name]" -eq "NoRepeatingAttributeLoaded"|_GACMB_DONE
  --?"[*R:global_skill_active_flag]" -eq "1"|>_AddGSKMB;[*R:global_skill_name];[&GAM_Button_Active_Color]
  --?"[*R:global_skill_active_flag]" -ne "1"|>_AddGSKMB;[*R:global_skill_name];[&GAM_Button_Inactive_Color]  
  --Rnext| 
  --?"[*R:global_skill_name]" -ne "NoRepeatingAttributeLoaded"|_GSKMB_Loop
  --:_GSKMB_DONE|
  --&[%2%]|[&_GSKMB_Temp] 
  --<|  

  --:_AddGSMB| 
  --&_GSMB_Temp|+[rbutton:#ffffff:#[%2%]:8px][%1%]::TOGGLE_GSM;[%1%][/rbutton][&SPACE]
  --<|  

  --:_AddGACMB| 
  --&_GACMB_Temp|+[rbutton:#ffffff:#[%2%]:8px][%1%]::TOGGLE_GACM;[%1%][/rbutton][&SPACE]
  --<|  

  --:_AddGSKMB| 
  --&_GSKMB_Temp|+[rbutton:#ffffff:#[%2%]:8px][%1%]::TOGGLE_GSKM;[%1%][/rbutton][&SPACE]
  --<|  

  --:Get_Ability_Modifier|attribute_string;returnnumericvalue;returnnamestring
  --=[%2%]|
  --&[%3%]|
  --?"[%1%]" -eq "strength_mod"|=[%2%];[*S:strength_mod]
  --?"[%1%]" -eq "strength_mod"|&[%3%];[STR]
  --?"[%1%]" -eq "intelligence_mod"|=[%2%];[*S:intelligence_mod]
  --?"[%1%]" -eq "intelligence_mod"|&[%3%];[INT]
  --?"[%1%]" -eq "wisdom_mod"|=[%2%];[*S:wisdom_mod]
  --?"[%1%]" -eq "wisdom_mod"|&[%3%];[WIS]
  --?"[%1%]" -eq "dexterity_mod"|=[%2%];[*S:dexterity_mod]
  --?"[%1%]" -eq "dexterity_mod"|&[%3%];[DEX]
  --?"[%1%]" -eq "constitution_mod"|=[%2%];[*S:constitution_mod]
  --?"[%1%]" -eq "constitution_mod"|&[%3%];[CON]
  --?"[%1%]" -eq "charisma_mod"|=[%2%];[*S:charisma_mod]
  --?"[%1%]" -eq "charisma_mod"|&[%3%];[CHA]
  --<|

  --/|There are four different options for applying damage. The selected option is set in the settings area of the macro.
  --/|When applying damage, one of the four functions listed below will be executed to alter the target's HP. The ScriptCards
  --/|method is recommended, as there won't be an issue with impacting the same target more than once.

  --/|Apply damage via ScriptCards' built-in object modification function (new in ScriptCards 1.5.0)
  --:ApplyDamageScriptCards|Parameters are tokenid;bar#;amount
  --=applyValue|[%3%]
  --=newValue|[*[%1%]:t-bar[%2%]_value] + [$applyValue]
  --?[$newValue] -lt 0|=newValue;0
  --?[$newValue] -gt [*[%1%]:t-bar[%2%]_max]|=newValue;[*[%1%]:t-bar[%2%]_max]
  --!t:[%1%]|bar[%2%]_value:[$newValue.Raw]
  --<|

  --/|Token-Mod doesn't have a "set to a relative number", so we get the current bar value, subtract thedamage amount
  --/|make sure it doesn't fall below 0, and set the amount back into the bar.
  --:ApplyDamageTokenmod|Parameters are tokenid;bar#;amount
  --=applyValue|[%3%]
  --=newValue|[*[%1%]:t-bar[%2%]_value] + [$applyValue]
  --?[$newValue] -lt 0|=newValue;0
  --?[$newValue] -gt [*[%1%]:t-bar[%2%]_max]|=newValue;[*[%1%]:t-bar[%2%]_max]
  --@token-mod|_ignore-selected _ids [%1%] _set bar[%2%]_value|[$newValue.Raw]
  --<| 
  
  --/|AlterBars supports relative values for the amount parameter, so we just pass it the value with a "-" in front of it.
  --:ApplyDamageAlterbars|Parameters are tokenid;bar#;amount
  --=applyValue|[%3%]
  --?[$applyValue] -lt 0|=applyValue;[$applyValue] {NEG}
  --@alter|_target|[%1%] _bar|[%2%] _amount|[%3%] _show|none
  --<|

  --/|Just a dummy routine that doesn't do anything, but it is easier to do it this way than adding logic to see if we are
  --/|skipping applying damage in the main routine.
  --:DontApplyDamage|
  --<|

  --:SHOW_DAMAGE_BUTTONS|DamageAmount
  --=FullDamage|[%3%]
  --=HalfDamage|[%3%] {NEGATE} \ 2 {NEGATE}
  --=QuarterDamage|[%3%] {NEGATE} \ 4 {NEGATE}
  --=HealDamage|[%3%] {NEGATE}
  --?[&whisperDamageButtonsToDM] -eq 1|[
    --*|[c][rbutton]Full ([$FullDamage.Raw])::APPLY_DAMAGE;[$FullDamage.Raw][/rbutton] | [rbutton]1/2 ([$HalfDamage.Raw])::APPLY_DAMAGE;[$HalfDamage.Raw][/rbutton] | [rbutton]1/4 ([$QuarterDamage.Raw])::APPLY_DAMAGE;[$QuarterDamage.Raw][/rbutton] &nbsp;&nbsp;|&nbsp;&nbsp; [rbutton]Heal::APPLY_DAMAGE;[$HealDamage.Raw][/rbutton][/c]
  --]|[
    --+|[c][rbutton]Full::APPLY_DAMAGE;[$FullDamage.Raw][/rbutton] | [rbutton]1/2::APPLY_DAMAGE;[$HalfDamage.Raw][/rbutton] | [rbutton]1/4::APPLY_DAMAGE;[$QuarterDamage.Raw][/rbutton] &nbsp;&nbsp;|&nbsp;&nbsp; [rbutton]Heal::APPLY_DAMAGE;[$HealDamage.Raw][/rbutton][/c]      
  --]|
  --<|

  --/|Reentry point to apply damage/healing to selected tokens
  --:APPLY_DAMAGE|Amount
  --?[&whisperDamageButtonResultsToDM] -eq 1|[
    --#whisper|gm
    --#title|Damage Applied
  --]|[
    --#whisper|nobody
    --#title|Should never show
  --]|
  --~tokenCount|array;getlength;SC_SelectedTokens
  --=tokenCount|[&tokenCount] - 1
  --%loop|0;[$tokenCount.Raw]
    --&thisToken|[@SC_SelectedTokens([&loop])]
    --+Applying|[&reentryval] HP to [*[&thisToken]:t-name]
    -->ApplyDamageScriptCards|[&thisToken];[&hitPointsBar];[&reentryval]
  --%|
  --X|

  --:FIND_REPEATING_RESOURCE|character_id;resourceName;resourceReturnNameStringVariable;ResourceReturnValueStringVariable;ResouceCurrentValue
    --?"[%2%]" -eq "[*[%1%]:class_resource_name]"|_IsResourceClass
    --?"[%2%]" -eq "[*[%1%]:other_resource_name]"|_IsResourceOther
    --Rfirst|[%1%];repeating_resource
    --:_ResourceLoop|
    --?"[*R:resource_left_name]" -eq "[%2%]"|_IsResourceLeft
    --?"[*R:resource_right_name]" -eq "[%2%]"|_IsResourceRight
    --Rnext|
    --?"[*R:resource_left_name]" -ne "NoRepeatingAttributeLoaded"|_ResourceLoop
    --&[%3%]|NotFound --&[%4%]|NotFound --&[%5%]|0 
  --<|
  
  --:_IsResourceClass| --&[%3%]|class_resource_name --&[%4%]|class_resource --&[%5%]|[*[%1%]:class_resource] --<|
  --:_IsResourceOther| --&[%3%]|other_resource_name --&[%4%]|other_resource --&[%5%]|[*[%1%]:other_resource] --<|
  --:_IsResourceLeft| --&[%3%]|[*R:resource_left_name]  --&[%4%]|[*R>resource_left] --&[%5%]|[*S:[*R>resource_left]]    --<|  
  --:_IsResourceRight| --&[%3%]|[*R:resource_left_name] --&[%4%]|[*R>resource_right] --&[%5%]|[*S:[*R>resource_right]] --<|

  --:_NO_AMMO_AVAILABLE|
  --#whisper|
  --+|[*S:character_name] has no [&ResourceUsed] left.
  --#emoteText|[*S:character_name] is all out of [&ResourceUsed].
  --X|

  --:FIND_CLASS_LEVEL|class;variableToSet
  --&[%2%]|0
  --?"[*S:class]" -eq "[%1%]"|&[%2%];[*S:base_level]
  --?"[*S:multiclass1]" -eq "[%1%]"|&[%2%];[*S:multiclass1_lvl]
  --?"[*S:multiclass2]" -eq "[%1%]"|&[%2%];[*S:multiclass2_lvl]
  --?"[*S:multiclass3]" -eq "[%1%]"|&[%2%];[*S:multiclass3_lvl]
  --<|

  --:GET_AVAILABLE_SPELL_SLOTS|slotlevel;variableToSet
  --&_maxSlots|[*S:lvl[%1%]_slots_total]
  --=_availSlots|[&_maxSlots] - [*S:lvl[%1%]_slots_expended]
  --&[%2%]|[*S:lvl[%1%]_slots_expended]
  --<|

  --:DEDUCT_SPELL_SLOT|slotlevel
  --!a:[*S:character_id]|lvl[%1%]_slots_expended:-=1
  --<|

  --:GET_HIGHEST_LEVEL_SPELL_SLOT|variableToSet
  --&[%1%]|0
  --%levelCounter|1;9
    -->GET_AVAILABLE_SPELL_SLOTS|[&levelCounter];slotTest
    --?[&slotTest] -gt 0|&[%1%];[&levelCounter]
  --%|
  --<|

  --:TOGGLE_GAM|GAM Name
  --Rfind|[*S:character_id];[&reentryval];repeating_tohitmod;global_attack_name
  --?"[*R:global_attack_name]" -eq "NoRepeatingAttributeLoaded"|_TOGGLE_ERROR
  --?"[*R:global_attack_active_flag]" -eq "1"|>_setGlobalMod;[*R>global_attack_active_flag];0|>_setGlobalMod;[*R>global_attack_active_flag];1
  --^proc_pc|
  --X|

  --:TOGGLE_GDM|GDM Name
  --Rfind|[*S:character_id];[&reentryval];repeating_damagemod;global_damage_name
  --?"[*R:global_damage_name]" -eq "NoRepeatingAttributeLoaded"|_TOGGLE_ERROR
  --?"[*R:global_damage_active_flag]" -eq "1"|>_setGlobalMod;[*R>global_damage_active_flag];0|>_setGlobalMod;[*R>global_damage_active_flag];1
  --^proc_pc|
  --X|

  --:TOGGLE_GSM|GSM Name
  --Rfind|[*S:character_id];[&reentryval];repeating_savemod;global_save_name
  --?"[*R:global_save_name]" -eq "NoRepeatingAttributeLoaded"|_TOGGLE_ERROR
  --?"[*R:global_save_active_flag]" -eq "1"|>_setGlobalMod;[*R>global_save_active_flag];0|>_setGlobalMod;[*R>global_save_active_flag];1
  --^proc_pc|
  --X|

  --:TOGGLE_GACM|GACM Name
  --Rfind|[*S:character_id];[&reentryval];repeating_acmod;global_ac_name
  --?"[*R:global_ac_name]" -eq "NoRepeatingAttributeLoaded"|_TOGGLE_ERROR
  --?"[*R:global_ac_active_flag]" -eq "1"|>_setGlobalMod;[*R>global_ac_active_flag];0|>_setGlobalMod;[*R>global_ac_active_flag];1
  --^proc_pc|
  --X|  

  --:TOGGLE_GSKM|GACM Name
  --Rfind|[*S:character_id];[&reentryval];repeating_skillmod;global_skill_name
  --?"[*R:global_skill_name]" -eq "NoRepeatingAttributeLoaded"|_TOGGLE_ERROR
  --?"[*R:global_skill_active_flag]" -eq "1"|>_setGlobalMod;[*R>global_skill_active_flag];0|>_setGlobalMod;[*R>global_skill_active_flag];1
  --^proc_pc|
  --X|  

  --:_setGlobalMod|name;value
  --!a:[&saveTokenID]|[%1%]:[%2%]
  --<|

  --:_TOGGLE_ERROR|Bad name
  --X|

  --:SUBSTITUTE_GLOBAL_MOD_IMAGE|mod name;ReturnVariableName
  --&[%2%]|[%1%]
  --~SGMI_TEMP|string;tolowercase;[%1%]
  --#parameterseparator|$
  --?"[&SGMI_TEMP]" -eq "bless"|&[%2%]$&#128519;
  --#parameterseparator|;
  --<|

  --:BuildCharacterSummary|charid;Return variable;ac attribute (either ac or npc_ac)
  --?[%3%] -eq "npc_ac"|BuildNPCSummary
  --&[%2%]|[t width='100%' align='left'][tr][td valign='top' align='left'][p style='font-size:8pt; margin-top:2px; margin-left:3px; margin-bottom:3px; line-height:1.05em;'][*[%1%]:race] [*[%1%]:class_display][/p][/td][td valign='top' align='right' width='60%'][p style='font-size:8pt; margin-top:2px; margin-right:3px; margin-bottom:3px; line-height:1.05em;'][&ACImage][*[%1%]:[%3%]], [&HPImage][*[%1%]:hp]/[*[%1%]:hp^], [&SpeedImage][*[%1%]:speed][/p][/td][/tr]
  -->REPLACE_WORDS_WITH_EMOJIS|[%2%]
  --<|
  --:BuildNPCSummary|
  --&[%2%]|[t width='100%' align='left'][tr][td valign='top' align='left'][p style='font-size:8pt; margin-top:2px; margin-left:3px; margin-bottom:3px; line-height:1.05em;']CR[*[%1%]:npc_challenge] [*[%1%]:npc_type][/p][/td][td valign='top' align='right' width='60%'][p style='font-size:8pt; margin-top:2px; margin-right:3px; margin-bottom:3px; line-height:1.05em;'][&ACImage][*[%1%]:[%3%]], [&HPImage][*[%1%]:t-bar[&hitPointsBar]_value]/[*[%1%]:t-bar[&hitPointsBar]_max], [&SpeedImage][*[%1%]:npc_speed][/p][/td][/tr][/t]
  -->REPLACE_WORDS_WITH_EMOJIS|[%2%]
  --<|
 
  --:CheckDamageModifiersNPC|characterid;damageVariableName;damageType;isMagical(0 or 1);IsSilver (0 or 1);IsAdamantine (0 or 1);NPCType(A or T)
  --&[%2%]|
  --#parameterdelimiter|!!
  --~Vulns|string!!split!!;!![*[%1%]:npc_vulnerabilities];[&[%7%]NPC_VULNS]
  --~Resists|string!!split!!;!![*[%1%]:npc_resistances];[&[%7%]NPC_RESISTS]
  --~Immunes|string!!split!!;!![*[%1%]:npc_immunities];[&[%7%]NPC_IMMUNES]
  --#parameterdelimiter|;
  --%VulnLoop|1;[$VulnsCount]
    --&hasModifier|0
    --?"[&Vulns[&VulnLoop]]" -inc "silver" -or "[&Vulns[&VulnLoop]]" -inc "adamantine" -or "[&Vulns[&VulnLoop]]" -inc "magical"|&hasModifier;1
    --?"[&Vulns[&VulnLoop]]" -inc "[%3%]" -and "[&Vulns[&VulnLoop]]" -inc "adamantine" -and [%6%] -eq 1|>IsVulnerable;[%2%]
    --?"[&Vulns[&VulnLoop]]" -inc "[%3%]" -and "[&Vulns[&VulnLoop]]" -inc "silver" -and [%5%] -eq 1|>IsVulnerable;[%2%]
    --?"[&Vulns[&VulnLoop]]" -inc "[%3%]" -and "[&Vulns[&VulnLoop]]" -inc "magical" -and [%4%] -eq 1|>IsVulnerable;[%2%]
    --?"[&Vulns[&VulnLoop]]" -inc "[%3%]" -and [&hasModifier] -eq 0|>IsVulnerable;[%2%]
  --%|
  --:SKIP_VULN_CHECK|
  --%ResistLoop|1;[$ResistsCount]
    --&hasModifier|0
    --?"[&Resists[&ResistLoop]]" -inc "silver" -or "[&Resists[&ResistLoop]]" -inc "adamantine" -or "[&Resists[&ResistLoop]]" -inc "magical"|&hasModifier;1
    --?"[&Resists[&ResistLoop]]" -inc "[%3%]" -and [&hasModifier] -eq 0|>IsResistant;[%2%]
    --?"[&Resists[&ResistLoop]]" -inc "[%3%]" -and [&hasModifier] -eq 0|FINISH_RESIST_CHECK   
    --?"[&Resists[&ResistLoop]]" -inc "[%3%]" -and "[&Resists[&ResistLoop]]" -inc "adamantine" -and [%6%] -eq 1|FINISH_RESIST_CHECK
    --?"[&Resists[&ResistLoop]]" -inc "[%3%]" -and "[&Resists[&ResistLoop]]" -inc "silver" -and [%5%] -eq 1|FINISH_RESIST_CHECK
    --?"[&Resists[&ResistLoop]]" -inc "[%3%]" -and "[&Resists[&ResistLoop]]" -inc "magical" -and [%4%] -eq 1|FINISH_RESIST_CHECK
    --?"[&Resists[&ResistLoop]]" -inc "[%3%]"|>IsResistant;[%2%]
    --:FINISH_RESIST_CHECK|
  --%|
  --:SKIP_RESIST_CHECK|
  --%ImmuneLoop|1;[$ImmunesCount]
    --&hasModifier|0
    --?"[&Immunes[&ImmuneLoop]]" -inc "silver" -or "[&Immunes[&ImmuneLoop]]" -inc "adamantine" -or "[&Immunes[&ImmuneLoop]]" -inc "magical"|&hasModifier;1
    --?"[&Immunes[&ImmuneLoop]]" -inc "[%3%]" -and [&hasModifier] -eq 0|>IsImmune;[%2%]
    --?"[&Immunes[&ImmuneLoop]]" -inc "[%3%]" -and [&hasModifier] -eq 0|FINISH_IMMUNE_CHECK
    --?"[&Immunes[&ImmuneLoop]]" -inc "[%3%]" -and [&hasModifier] -eq 1 -and "[&Immunes[&ImmuneLoop]]" -inc "adamantine" -and [%6%] -eq 1|FINISH_IMMUNE_CHECK
    --?"[&Immunes[&ImmuneLoop]]" -inc "[%3%]" -and [&hasModifier] -eq 1 -and "[&Immunes[&ImmuneLoop]]" -inc "silver" -and [%5%] -eq 1|>FINISH_IMMUNE_CHECK;[%2%]
    --?"[&Immunes[&ImmuneLoop]]" -inc "[%3%]" -and [&hasModifier] -eq 1 -and "[&Immunes[&ImmuneLoop]]" -inc "magical" -and [%4%] -eq 1|>FINISH_IMMUNE_CHECK;[%2%]
    --?"[&Immunes[&ImmuneLoop]]" -inc "[%3%]"|>IsImmune;[%2%]
    --:FINISH_IMMUNE_CHECK|
  --%|
  --:SKIP_IMMUNE_CHECK|
  --<|
  --:IsVulnerable| --&[%1%]| * 2 [Vulnerable] --<|
  --:IsResistant| --&[%1%]| \ 2 [Resistant] --<|
  --:IsImmune| --&[%1%]| * 0 [Immune] --<|

  --:DISPLAY_CHARACTER_CONFIG_MENU|
  --#whisper|self
  --#rightsub|
  --#leftsub|Action Menu Configuration [rbutton]&#x21b6;::AFTER_ADVMODE_CHANGE[/rbutton]

  --+|[b][c]Display Global Mods[/c][/b]
  -->CREATE_SETTINGS_TOGGLE_BUTTON|[*S:character_id];Attack;SGAM;BTN_SGAM
  -->CREATE_SETTINGS_TOGGLE_BUTTON|[*S:character_id];Damage;SGDM;BTN_SGDM
  -->CREATE_SETTINGS_TOGGLE_BUTTON|[*S:character_id];Save;SGSM;BTN_SGSM
  -->CREATE_SETTINGS_TOGGLE_BUTTON|[*S:character_id];AC;SGCM;BTN_SGAC
  -->CREATE_SETTINGS_TOGGLE_BUTTON|[*S:character_id];Skill;SGKM;BTN_SGSK
  --+|[c][&BTN_SGAM] [&BTN_SGDM] [&BTN_SGSM] [&BTN_SGAC] [&BTN_SGSK][/c]
  --+|[b][c]Display Skill and Saves Buttons[/c][/b]
  -->CREATE_SETTINGS_TOGGLE_BUTTON|[*S:character_id];Skills & Saves;SSAS;BTN_SSAS
  --+|[c][&BTN_SSAS][/c]
  -->CREATE_SETTINGS_TOGGLE_BUTTON|[*S:character_id];Rest Buttons in Header;SHRB;BTN_SHRB
  --+|[b][c]Other Features[/c][/b]
  --+|[c][&BTN_SHRB][/c]
  --X|

  --:CREATE_SETTINGS_TOGGLE_BUTTON|character_id;SettingName;SettingCode;ReturnStringVar
  -->GET_PER_CHARACTER_SETTING|[%1%];[%3%];_THIS_SETTING
  --&_BUTTON_COLOR|[&GAM_Button_Inactive_Color]
  --?[&_THIS_SETTING] -eq 1|&_BUTTON_COLOR;[&GAM_Button_Active_Color]
  --&[%4%]|[rbutton:#ffffff:#[&_BUTTON_COLOR]:8px][%2%]::TOGGLE_PER_CHARACTER_SETTING;[%1%]|[%3%][/rbutton]
  --<|

  --:GET_PER_CHARACTER_SETTING|CharID;SettingCode;Return String Var Name
  --&[%3%]|0
  --?"[*[%1%]:5ECAM_SETTINGS]" -inc "[%2%]1"|&[%3%];1
  --<|

  --:LOAD_PER_CHARACTER_CONFIG_OPTIONS|characterID
  --?"[*[%1%]:5ECAM_SETTINGS]" -eq ""|>SET_DEFAULT_PER_CHARACTER_SETTINGS;[%1%]
  --?"[*[%1%]:5ECAM_SETTINGS]" -eq "undefined"|>SET_DEFAULT_PER_CHARACTER_SETTINGS;[%1%]
  --&perCharacterOptions|[*[%1%]:5ECAM_SETTINGS]
  --<|

  --:SET_DEFAULT_PER_CHARACTER_SETTINGS|CharacterID
  --&SettingsString|$SGAM1$$SGDM1$$SGSM1$$SGMB1$$SSAS1$$SGCM1$$SGKM1$$SHRB1$
  --!a:[%1%]|!5ECAM_SETTINGS:[&SettingsString]
  --<|

  --:TOGGLE_PER_CHARACTER_SETTING|CharacterID;Setting
  --~TOG_CHAR_ID|string;before;|;[&reentryval]
  --~TOG_SETTING|string;after;|;[&reentryval]
  --~beforeText|string;before;[&TOG_SETTING];[*[&TOG_CHAR_ID]:5ECAM_SETTINGS]
  --~beforeLength|string;length;[&beforeText]
  --~wholeLength|string;length;[*[&TOG_CHAR_ID]:5ECAM_SETTINGS]
  --?[$beforeLength] -eq [$wholeLength]|SETTING_NOT_FOUND
  --~oldSetting|string;substring;[$beforeLength];7;[*[&TOG_CHAR_ID]:5ECAM_SETTINGS]
  --~oldValue|string;substring;6;1;[&oldSetting]
  --~newSetting|string;left;5;[&oldSetting]
  --?"[&oldValue]" -eq "1"|&newSetting;+0$
  --?"[&oldValue]" -ne "1"|&newSetting;+1$
  --~replacement|string;replace;[&oldSetting];[&newSetting];[*[&TOG_CHAR_ID]:5ECAM_SETTINGS]
  --!a:[&TOG_CHAR_ID]|5ECAM_SETTINGS:[&replacement]
  --^DISPLAY_CHARACTER_CONFIG_MENU|
  --:SETTING_NOT_FOUND|
  --!a:[&TOG_CHAR_ID]|5ECAM_SETTINGS:[*[&TOG_CHAR_ID]:5ECAM_SETTINGS]$[&TOG_SETTING]1$
  --^DISPLAY_CHARACTER_CONFIG_MENU|
  --X|

  --:ADD_STATUS_MARKER|Token_ID;Status Marker;Count
  -->REMOVE_STATUS_MARKER|[%1%];[%2%]
  --~|array;statusmarkers;Conditions;[%1%]
  --&toAdd|[%2%]
  --?"[%3%]" -ne "" -and "[%3%]" -ne "0"|&toAdd;[%2%]@[%3%]
  --~|array;add;Conditions;[&toAdd]
  --~newConditions|array;stringify;Conditions
  --#parameterdelimiter|$
  --~newConditions|string$replaceall$;$,$[&newConditions]
  --#parameterdelimiter|;
  --!t:[%1%]|statusmarkers:[&newConditions]
  --:DONE_ADD_STATUS_MARKER|
  --<|

  --:REMOVE_STATUS_MARKER|Token_ID;Status Marker
  -->CHECK_STATUS_MARKER|[%1%];[%2%];MarkerExists;MarkerCounter  
  --?[&MarkerExists] -eq 0|<
  --~|array;statusmarkers;Conditions;[%1%]
  --?[&MarkerCounter] -eq 0|&toCheckFor;[%2%]|&toCheckFor;[%2%]@[&MarkerCounter]
  --~hasCondition|array;indexof;Conditions;[&toCheckFor]
  --?[&hasCondition] -eq "ArrayError"|DONE_REMOVE_STATUS_MARKER
  --~|array;remove;Conditions;[&toCheckFor]
  --~newConditions|array;stringify;Conditions
  --#parameterdelimiter|$
  --~newConditions|string$replaceall$;$,$[&newConditions]
  --#parameterdelimiter|;
  --!t:[%1%]|statusmarkers: [&newConditions]
  --:DONE_REMOVE_STATUS_MARKER|
  --<|  
  
  --:CHECK_STATUS_MARKER|TokenID;MarkerToFind;Exists;Counter
  --~|array;statusmarkers;Conditions;[%1%]
  --&FoundMarker|-1
  --%loop|0;[@Conditions(maxindex)]
    --&Temp|[@Conditions([&loop])]
	--?[&Temp(indexof,[%2%])] -eq 0|&FoundMarker;[&loop]
  --%|
  --?[&FoundMarker] -ne -1|FOUND_STATUS_MARKER
  --&[%3%]|0 --&[%4%]|0 --<|
  --:FOUND_STATUS_MARKER|
  --&[%3%]|1
  --&[%4%]|0
  --&ThisMarker|[@Conditions([&FoundMarker])]
  --?[&ThisMarker(contains,@)] -eq 0|<
  --~[%4%]|string;after;@;[@Conditions([&FoundMarker])]
  --<|

  --:GET_CONTROLLED_TOKENS_FROM_PAGETOKENS|
  --~|array;pagetokens;_FCT_Tokens;[*C:playerpageid];pc
  --%_loop|0;[@_FCT_Tokens(maxindex)]
    --&_FCT_TID|[@_FCT_Tokens([&_loop])]
    --&_FCT_CHAR|[*[&_FCT_TID]:t-represents]
    --?"[*O:[&_FCT_CHAR]:character:controlledby]" -ninc [&SendingPlayerID]|%
    --?"[&[%1%]]" -eq ""|&[%1%];+[&_FCT_TID]|&[%1%];+,[&_FCT_TID]]
  --%|
  --<|

  --:REPORT_NEED_SELECTED_TOKEN|
  --#whisper|self
  --+No Token Selected|You need to have a single token selected in order to use the 5E Character Action Menu.
  --X|

  --:REPORT_BAD_SC_VERSION|
  --#whisper|self
  --+NOTE|This version of the 5E Character action Menu requires ScriptCards version 2.6.6b or higher. Please update your SC installation.
  --X|

  --:REPORT_MULTIPLE_SELECTED_TOKEN|
  --#whisper|self
  --+Multiple Tokens Selected|You need to have a single token selected in order to use the 5E Character Action Menu.
  --X|

  --:REPORT_NO_TOKENS_ON_PAGE|
  --#whisper|self
  --+No Controlled Tokens|You do not appear to have any tokens on this page under your control.
  --X|

  --:REPORT_MULTIPLE_POSSIBLE_TOKENS|
  --#whisper|self
  --+Multiple Tokens|You appear to have more than one controlled token on this page. You will need to select a token manually to use the 5E Character Action Menu
  --X|

  --:DO_SHORT_REST|
  --#whisper|self
  --#title|[*S:character_name]
  --#leftsub|Short Rest
  --#rightsub|
  --&hitDieRolls|
  --=hitDiceUsed|0
  --=hitpointsRegained|0
  --:CONTINUE_SHORT_RESTING|
  --+Current Hit Points|[*S:hp]
  --+Max Hit Points|[*S:hp^]
  --+Hit Dice Available|[*S:hit_dice]
  --?"[&hitDieRolls]" -eq ""|SKIP_SHOW_HD_ROLLS
  --+Hit Die Roll(s)|[&hitDieRolls]
  --:SKIP_SHOW_HD_ROLLS|
  --?[*S:hit_dice] -lt 1|SKIP_SHOW_HITDIE_BUTTON
  --+|[c][rbutton]Spend Hit Die::SPEND_HIT_DIE[/rbutton][/c]
  --:SKIP_SHOW_HITDIE_BUTTON|
  --+|[c][rbutton]Complete Short Rest::COMPLETE_SHORT_REST[/rbutton][/c]
  --X|

  --:SPEND_HIT_DIE|
  --?[*S:hit_dice] -lt 1|CONTINUE_SHORT_RESTING
  --=HitDieRoll|1d[*S:hitdietype]
  --=HitDiceUsed|[$HitDiceUsed] + 1
  --=hitpointsRegained|[$hitpointsRegained] + [$HitDieRoll]
  --=NewHitPoints|[*S:hp] + [$HitDieRoll] {MAX:[*S:hp^]}
  --&hitDieRolls|+[$HitDieRoll][&SPACE]
  --!a:[&saveTokenID]|hp:[$NewHitPoints]
  --!a:[&saveTokenID]|hit_dice:-=1
  --^CONTINUE_SHORT_RESTING|

  --:COMPLETE_SHORT_REST|
  --#whisper|
  --#title|[*S:character_name]
  --#leftsub|Takes a short rest
  --#rightsub|
  --?[$hitDiceUsed] -eq 0|SKIP_HDUSED
  --+Hit Dice|[$hitDiceUsed.Raw] expended to regain [$hitpointsRegained.Raw] HP
  --:SKIP_HDUSED|
  -->RESTORE_WARLOCK_SPELLSLOTS|
  --X|

  --:DO_LONG_REST|
  --#whisper|
  --#title|[*S:character_name]
  --#leftsub|Takes a long rest
  --#rightsub|
  -->RESET_HITPOINTS|
  -->RESET_SPELL_SLOTS|
  -->RESTORE_HIT_DICE|
  --?"[&UseAMAShadowdarkSystem]" -ne "1"|LONG_REST_SKIP_SD
  -->_LibAMA_SHADOWDARK_SPELL_CLEAR_INNATE|[*S:character_id]
  --+|Rest: Failed spells restored
  --:LONG_REST_SKIP_SD|
  --X|

  --:RESET_HITPOINTS|
  --!a:[&saveTokenID]|hp:[*S:hp^]
  --+Rest:|All hitpoints healed
  --<|

  --:RESET_SPELL_SLOTS|
  --!a:[&saveTokenID]|lvl1_slots_expended:[*S:lvl1_slots_total]
  --!a:[&saveTokenID]|lvl2_slots_expended:[*S:lvl2_slots_total]
  --!a:[&saveTokenID]|lvl3_slots_expended:[*S:lvl3_slots_total]
  --!a:[&saveTokenID]|lvl4_slots_expended:[*S:lvl4_slots_total]
  --!a:[&saveTokenID]|lvl5_slots_expended:[*S:lvl5_slots_total]
  --!a:[&saveTokenID]|lvl6_slots_expended:[*S:lvl6_slots_total]
  --!a:[&saveTokenID]|lvl7_slots_expended:[*S:lvl7_slots_total]
  --!a:[&saveTokenID]|lvl8_slots_expended:[*S:lvl8_slots_total]
  --!a:[&saveTokenID]|lvl9_slots_expended:[*S:lvl9_slots_total]
  --+Rest:|Spell slots restored
  --<|

  --:RESTORE_HIT_DICE|
  --?[*S:hit_dice] -ge [*S:hit_dice^]|<
  --=HD_TO_RESTORE|[*S:level] / 2 {FLOOR} {MIN:1}
  --=NEW_HITDICE|[*S:hit_dice] + [$HD_TO_RESTORE] {MAX:[*S:hit_dice^]}
  --=REST_HD|[$NEW_HITDICE] - [*S:hit_dice]
  --!a:[&saveTokenID]|hit_dice:[$NEW_HITDICE.Raw]
  --+Rest:|Restored [$REST_HD] hit dice
  --<|

  --:RESTORE_WARLOCK_SPELLSLOTS|
  -->GET_WARLOCK_LEVEL|WarlockLevel
  --?[&WarlockLevel] -eq 0|<
  --=warlockSlotLevel|[&WarlockLevel] / 2 {CEIL} {MAX:5}
  --&NumWarlockSlots|1
  --?[&WarlockLevel] -ge 1|&NumWarlockSlots;2
  --?[&WarlockLevel] -ge 11|&NumWarlockSlots;3
  --?[&WarlockLevel] -ge 14|&NumWarlockSlots;4
  --=NewWarlockSlotExpended|[*S:lvl[$warlockSlotLevel.Raw]_slots_expended] + [&NumWarlockSlots] {MAX:[*S:lvl[$warlockSlotLevel.Raw]_slots_total]}
  --?[*S:lvl[$warlockSlotLevel.Raw]_slots_expended] -eq [$NewWarlockSlotExpended.Raw]|<
  --!a:[&saveTokenID]|lvl[$warlockSlotLevel.Raw]_slots_expended:[$NewWarlockSlotExpended.Raw]
  --+Rest:|[&NumWarlockSlots] level [$warlockSlotLevel.Raw] Pact Magic slots restored
  --<|

  --:GET_WARLOCK_LEVEL|LevelVariable
  --&[%1%]|0
  --?[*S:class] -eq "Warlock"|&[%1%];[*S:base_level]
  --?"[*S:multiclass1_flag]" -eq "1" -and [*S:multiclass1] -eq "Warlock"|&[%1%];[*S:multiclass1_lvl]
  --?"[*S:multiclass2_flag]" -eq "1" -and [*S:multiclass2] -eq "Warlock"|&[%1%];[*S:multiclass2_lvl]
  --?"[*S:multiclass3_flag]" -eq "1" -and [*S:multiclass3] -eq "Warlock"|&[%1%];[*S:multiclass2_lvl]
  --<|

  --:CHECK_SPELLS_TO_KEEP|AttackName; Return variable;suffix
  --~index|array;indexof;SpellsToKeepWithSuffix;[%1%]
  --~index2|array;indexof;SpellsToKeepNoSuffix;[%1%]
  --?"[&index]" -eq "ArrayError" -and "[&index2]" -eq "ArrayError"|<
  --?"[&index]" -ne "ArrayError"|&[%3%]; [After Cast]
  --&[%2%]|Y
  --<|

  --:DETERMINE_NPC_STATUS_MODIFIERS|TokenID;A for Attacker or T for Target
  --&[%2%]NPC_ATKMOD| --&[%2%]NPC_DMGMOD| --&[%2%]NPC_ACMOD| --&[%2%]NPC_ROLLMODE| --&[%2%]NPC_SAVEMODE_str| --&[%2%]NPC_SAVEMODE_dex| --&[%2%]NPC_SAVEMODE_con|
  --&[%2%]NPC_SAVEMODE_int| --&[%2%]NPC_SAVEMODE_wis| --&[%2%]NPC_SAVEMODE_cha| --&[%2%]NPC_RESISTS| --&[%2%]NPC_VULNS| --&[%2%]NPC_IMMUNES|
  --&[%2%]NPC_ATTACKAGAINSTMODE|
  --?[&useTokenMarkersForNPCModifiers] -eq "0"|<
  --?"[*[%1%]:t-statusmarkers]" -eq ""|<
  --~|array;fromstring;tokenMarkers;,;[*[%1%]:t-statusmarkers]
  --%loop|foreach;tokenMarkers
    --~markerName|string;before;@;[&loop]
    --?"[&statusmod_[&markerName]]" -eq ""|%
    --~param|string;split;:;[&statusmod_[&markerName]]
    --?[&param1] -eq "attack"|&[%2%]NPC_ATKMOD;+ [&param2]
    --?[&param1] -eq "damage"|&[%2%]NPC_DMGMOD;+ [&param2]
    --?[&param1] -eq "ac"|&[%2%]NPC_ACMOD;+ [&param2]
    --?[&param1] -eq "rollmode" -and [&param2] -eq "d"|&[%2%]NPC_ROLLMODE;2d20kl1
    --?[&param1] -eq "rollmode" -and [&param2] -eq "a"|&[%2%]NPC_ROLLMODE;2d20kh1
    --?[&param1] -eq "rollmode" -and [&param2] -eq "n"|&[%2%]NPC_ROLLMODE;2d20
    --?[&param1] -eq "attackermode" -and [&param2] -eq "d"|&[%2%]NPC_ATTACKAGAINSTMODE;2d20kl1
    --?[&param1] -eq "attackermode" -and [&param2] -eq "a"|&[%2%]NPC_ATTACKAGAINSTMODE;2d20kh1
    --?[&param1] -eq "attackermode" -and [&param2] -eq "n"|&[%2%]NPC_ATTACKAGAINSTMODE;2d20
    --?[&param1] -eq "resist"|&[%2%]NPC_RESISTS;+ [&param2]
    --?[&param1] -eq "immune"|&[%2%]NPC_IMMUNES;+ [&param2]
    --?[&param1] -eq "vuln"|&[%2%]NPC_VULNS;+ [&param2]
    --?[&param1] -ne "savemode"|%
    --?"[*S:[%1%]]" -inc advantage|&[%2%]NPC_ROLLMODE;2d20kh1
    --?"[*S:[%1%]]" -inc disadvantage|&[%2%]NPC_ROLLMODE;2d20kl1
    --~|array;fromstring;savelist;-;[&param3]
    --%saves|foreach;savelist
        --?[&param2] -eq "d"|&[%2%]NPC_SAVEMODE_[&saves];2d20kl1
        --?[&param2] -eq "a"|&[%2%]NPC_SAVEMODE_[&saves];2d20kh1
        --?[&param2] -eq "n"|&[%2%]NPC_SAVEMODE_[&saves];2d20
    --%|
  --%|
  --<|

  --:DISPLAY_ACTIONMENU_CHARACTER_ABILITIES|Character ID
  --&DACA_BUTTONS|
  --~|array;objects:ability;AbilArray;AM-;[%1%]
  --~|array;define;DACA_OBJS_ID
  --?"[@AbilArray(length)]" -eq "0"|DONE_CHAR_SPECIFIC_BUTTONS
  --%loop|foreach;AbilArray
    --&DACA_OBJS_ID(9999)|[*O:[&loop]:ability:name]|[&loop]
  --%|

  --:DONE_CHAR_SPECIFIC_BUTTONS|character_id
  --/|Note : due to API issues, this currently does not work. Skipping over this section for now
  --^DONE_SHARED_MULE_BUTTONS|

  --/|Check for shared macro mule macros
  --\>GET_CHARACTER_ID_BY_NAME|[&sharedmacromulename];MacroMuleID
  --?[&MacroMuleID] -eq ""|DONE_SHARED_MULE_BUTTONS
  --~|array;objects:ability;MuleButtonList;AM_SHARED_LIST;[%1%]
  --?"[@MuleButtonList(length)]" -eq 0|DONE_SHARED_MULE_BUTTONS
  --?[&muleButtonList] -eq "AbilityNotFound"|DONE_SHARED_MULE_BUTTONS
  --~|array;objects:character;macro_mule;[&sharedmacromulename]
  --?[@macro_mule(maxindex)] -lt 0|DONE_SHARED_MULE_BUTTONS

  --:DONE_SHARED_MULE_BUTTONS|
  --?"[@DACA_OBJS_ID(length)]" -eq "0"|<
  --~|array;sort;DACA_OBJS_ID
  --&DACA_BUTTONS|
  --%loop|foreach;DACA_OBJS_ID
    --~DACA_PARTS|string;split;|;[&loop]
    --&DACA_NAME|[&DACA_PARTS1(3)]
    --&DACA_BUTTONS|+[button][&DACA_NAME(replaceall,-, )]::~[%1%]|[&DACA_PARTS1][/button]
  --%|
  --?"[&DACA_BUTTONS]" -eq ""|<

  --+Custom Actions|
  --+|[&DACA_BUTTONS]
  --<|

  --:GET_CHARACTER_ID_BY_NAME|
  --~|array;objects:character;found_char_list;[%1%]
  --?[@found_char_list(maxindex)] -lt 0|&[%2%];0|&[%2%];[@found_char_list(0)]
  --<|

  --/|To customize for other games : remove or update the items between 5E SPECIFIC START and 5E SPECIFIC END

  --:BUILD_BUTTON_LIST|Character ID;Repeating_section_name;repeating field for name;reentrant point;section header;mule char name/id;filter out spells
  --~|hashtable;fromrepeatingsection;[%1%];[%2%];[%3%];BuiltButtonHash
  --~|array;fromkeys;Hash_Keys;BuiltButtonHash
  --?[@Hash_Keys(length)] -eq 0|AM_BUTTON_LIST_NOTHING
  --~|array;define;HashList;dummy
  --~|array;removeat;HashList;0
  --%loop|foreach;Hash_Keys
    --?"[&loop]" -inc "_sectionid"|&HashList(99999);[&loop(replace,_sectionid,)]
  --%|

  --&AM_BUTTON_LIST|[br]
  --%atkloop|foreach;HashList
    --&DisplayName|[&atkloop]
    --&ActionName|[&DisplayName(replace,;,-!-!-)]
    --/|5E SPECIFIC START 
    --?"[&DisplayName(contains,Multiattack)]" -eq 1|>SHOW_MULTIATTACK;[&atkloop]_description
    --?"[&DisplayName(contains,Multiattack)]" -eq 1|AM_LIST_CONTINUE
    --?[%8%] -eq 0|SKIP_BUTTON_LIST_SPELL_CHECK
    --&keepSpell|N
    --&spellSuffix|
    -->CHECK_SPELLS_TO_KEEP|"[&atkloop]";keepSpell;spellSuffix
    --&spellLink|[:BuiltButtonHash("[&atkloop]_spelldesc_link")]
    --?"[&spellLink]" -eq ""|&spellLink;undefined
    --?"[&spellLink]" -ne "undefined" -and "[&keepSpell]" -eq "N"|AM_LIST_CONTINUE
    --:SKIP_BUTTON_LIST_SPELL_CHECK|
    --/|5E SPECIFIC END

    --?"[%6%]" -eq "none"|AM_LIST_SKIP_MULE_CHECK
    --~testName|string;replaceall; ;-;[&DisplayName]
    --~testMule|system;findability;[%6%];[&testName]
    --?[&testMule] -eq "AbilityNotFound"|AM_LIST_SKIP_MULE_CHECK
    --&AM_BUTTON_LIST|+[button][&DisplayName(totitlecase)]::~[%6%]|[&testName][/button][&SPACE]
    --^AM_LIST_CONTINUE|
    --:AM_LIST_SKIP_MULE_CHECK|
    --&AM_BUTTON_LIST|+[rbutton][&DisplayName(totitlecase)]::[%4%];[&ActionName]|[&TB]target|token_id}[/rbutton][&SPACE]
    --:AM_LIST_CONTINUE|
  --%|
  --+[%5%]|[&AM_BUTTON_LIST]
  --:AM_BUTTON_LIST_NOTHING|
  --<|

  --:BUILD_SPELL_BUTTON_LIST|Character ID;Repeating_section_name;repeating field for name;reentrant point;section header;mule char name/id;spell level
    --&slotString|
    --&RitualslotString|
    --&LevelString|[%7%]|
    --?"[%7%]" -eq 0|&LevelString; 
    --?"[%7%]" -eq 0|BEGIN_BUILDING_SPELL_BUTTONS
    --?"[&UseAMAShadowdarkSystem]" -eq "1"|BEGIN_BUILDING_SPELL_BUTTONS
    --?"[%7%]" -gt 0|>BUILD_SLOT_LIST;[%7%];slotString;
    --?"[%7%]" -gt 0|>BUILD_SLOT_LIST;[%7%];RitualslotString;Yes
    --:BEGIN_BUILDING_SPELL_BUTTONS|
    --~|hashtable;fromrepeatingsection;[%1%];[%2%];[%3%];BuiltButtonHash
    --~|array;fromkeys;Hash_Keys;BuiltButtonHash
    --?[@Hash_Keys(length)] -eq 0|AM_SPELL_BUTTON_LIST_NOTHING
    --~|array;define;HashList;dummy
    --~|array;removeat;HashList;0
    --%loop|foreach;Hash_Keys
      --?"[&loop]" -inc "_sectionid"|&HashList(99999);[&loop(replace,_sectionid,)]
    --%|

    --&AM_BUTTON_LIST|[br]

    --%spellloop|foreach;HashList
      --/\|Loop: [&spellloop]

      --?[&skipPreppedSpellsCheck] -eq 1|SKIP_PREP_CHECK
      --?"" -eq "1"|SKIP_PREP_CHECK
      --?"[%7%]" -eq 0|SKIP_PREP_CHECK   
      --?"[&SorcererLevel]" -gt 0 -or "[&WarlockLevel]" -gt 0|SKIP_PREP_CHECK
      --?"[:BuiltButtonHash("[&spellloop]_spellprepared")]" -eq "1" -or "[:BuiltButtonHash("[&spellloop]_spellritual")]" -eq "Yes"|SKIP_PREP_CHECK
      --^SPELL_BUTTON_END_GENERATE|
      --:SKIP_PREP_CHECK|
      --?"[&UseAMAShadowdarkSystem]" -eq "1" -and "[:BuiltButtonHash("[&spellloop]_innate")]" -eq 1|SPELL_BUTTON_END_GENERATE
      --&targetString|[&TB]target|token_id}
      --&DisplayName|[&spellloop]
      --&DisplayName|[&DisplayName(totitlecase)]
      --/\|DisplayName [&DisplayName]
      --&thisSlotString|[&slotString]
      --?"[:BuiltButtonHash("[&spellloop]_spellritual")]" -ne "Yes"|SPELL_BUTTON_LIST_NOT_RITUAL
      --&thisSlotString|[&RitualslotString]
      --&DisplayName|+ [R]
      --:SPELL_BUTTON_LIST_NOT_RITUAL|
      --?"[:BuiltButtonHash("[&spellloop]_spellcastingtime")]" -inc "reaction"|&DisplayName;+ [RA]
      --?"[:BuiltButtonHash("[&spellloop]_spellcastingtime")]" -inc "bonus"|&DisplayName;+ [BA]
      --?"[:BuiltButtonHash("[&spellloop]_spellconcentration")]" -inc "concentration=1"|&DisplayName;+ [C]
      --?"[%7%]" -eq 0|AM_SPELL_BUTTON_SKIP_SLOT_INFO 
      --?[&trackPCSpellSlots] -eq 0|AM_SPELL_BUTTON_SKIP_SLOT_INFO
      --?[&HighestSlot] -ge [%7%]|AM_SPELL_BUTTON_SKIP_SLOT_INFO
      --?"[:BuiltButtonHash("[&spellloop]_spellritual")]" -eq "Yes"|AM_SPELL_BUTTON_SKIP_SLOT_INFO
      --/|We are tracking spells and the caster doesn't have a high enough slot, and it isn't a ritual spell.
      --^SPELL_BUTTON_END_GENERATE|
      --:AM_SPELL_BUTTON_SKIP_SLOT_INFO|
      --~testName|string;replaceall; ;-;[&spellloop]
      --~testMule|system;findability;[&spellmulename];[&testName]
      --?[&testMule] -ne "AbilityNotFound"|SPELL_BUTTON_MULE
      --?"[:BuiltButtonHash("[&spellloop]_spelloutput")]" -eq ATTACK|SPELL_BUTTON_GENERATE_WITH_TARGET
      --&AM_BUTTON_LIST|+[rbutton][&DisplayName]::[%4%];[&LevelString][&spellloop]|NONE[&thisSlotString][/rbutton][&SPACE]
      --^SPELL_BUTTON_END_GENERATE|
      --:SPELL_BUTTON_GENERATE_WITH_TARGET|
      --&AM_BUTTON_LIST|+[rbutton][&DisplayName]::[%4%];[&LevelString][&spellloop]|[&targetString][&thisSlotString][/rbutton][&SPACE]
      --^SPELL_BUTTON_END_GENERATE|
      --:SPELL_BUTTON_MULE|
      --&AM_BUTTON_LIST|+[button][&DisplayName]::~[&spellmulename]|[&testName][/button][&SPACE]
      --:SPELL_BUTTON_END_GENERATE|
    --%|
      
    --/|If the list is empty, skip the output|
    --?"[&AM_BUTTON_LIST]" -eq ""|AM_SPELL_BUTTON_LIST_NOTHING
    --?"[&AM_BUTTON_LIST]" -eq "[br]"|AM_SPELL_BUTTON_LIST_NOTHING
    --+[%5%]|[&AM_BUTTON_LIST]
    --:AM_SPELL_BUTTON_LIST_NOTHING|
  --<|

  --:SHOW_MULTIATTACK|description
  --+Multiattack|[:PCAttacks("[%1%]")]
  --<|

  --:BUILD_SLOT_LIST|Spell Level;return string;is ritual (Yes for true)
  --&buildslotString|
  --?[%1%] -eq 9 -and "[%3%]" -eq ""|SKIP_BUILD_SLOT_LIST
  --&buildslotString||&#
  --&buildslotString|+63;{Slot Level|
  --?"[%3%]" -ne "Yes"|SLOT_LIST_NOT_RITUAL
  --&buildslotString|+As Ritual|
  --:SLOT_LIST_NOT_RITUAL|
  --%slotcounter|[%1%];9
    --&buildslotString|+[&slotcounter]
    --?[&slotcounter] -eq 9|SKIP_SLOT_LIST_VERTICAL_BAR
    --&buildslotString|+|
    --:SKIP_SLOT_LIST_VERTICAL_BAR|
  --%|
  --&buildslotString|+}
  --^BUILD_SLOT_LIST_DONE|
  --:SKIP_BUILD_SLOT_LIST|
  --&buildslotString||9
  --:BUILD_SLOT_LIST_DONE|
  --&[%2%]|[&buildslotString]
  --<|

  --:BUILD_TARGET_STRING|
  --&[%1%]|[&TB]target|token_id}
  --<|

  --:BUILD_SAVES_AND_SKILLS_BUTTONS|[%1%]
  --&advantageMenu|[&QB]Roll Type?|Normal|Advantage|Disadvantage[&QE]
  --&skillMenu|[&QB]Skill to roll?|Acrobatics|Animal Handling,animal_handling|Arcana|Athletics|Deception|History|Insight|Intimidation|Investigation|Medicine|Nature|Perception|Persuasion|Religion|Sleight of Hand,sleight_of_hand|Stealth|Survival[&QE]
  --=ToolCount|0
    --RFirst|[*S:character_id];repeating_tool
  --:reptoollist|
  --?"[*R:toolname]" -eq "NoRepeatingAttributeLoaded"|endToolMenu
    --=ToolCount|[$ToolCount.Raw] + 1
    --&toolMenu|+|[*R:toolname],[*R:toolname]
    --&saveFirstTool|[*R:toolname]
    --rNext|
  --?"[*R:toolname]" -ne "NoRepeatingAttributeLoaded"|reptoollist
  --:endToolMenu|
  --?[$ToolCount.Raw] -eq 1|[
    --&toolMenu|+[&vbar]
  --]|
  --&toolMenu|[&QB]Tool to roll?[&toolMenu][&QE]
  
  --&saveMenu|[&QB]Saving Throw Type?|Strength|Dexterity|Constitution|Intelligence|Wisdom|Charisma[&QE]
  --&toolButton|
  --?[$ToolCount] -gt 0|[
      --&toolButton|[&NB] [rbutton:Execute a Tool Check][&ToolCheckImage]::TOOL_CHECK;[&toolMenu]|[&advantageMenu][/rbutton] 
  --]|
  --&[%1%]|[rbutton:Execute a Skill Check][&SkillCheckImage]::SKILL_CHECK;[&skillMenu]|[&advantageMenu][/rbutton] [&toolButton] [&NB] [rbutton][&SavingThrowImage]::SAVING_THROW;[&saveMenu]|[&advantageMenu][/rbutton]
  --<|

  --:REPLACE_WORDS_WITH_EMOJIS|Input variable NAME (not the variable refernce). Will be updated in place
  --?[&UseEmojisToReplaceText] -ne 1|<
  --&work|[&[%1%]]
  --&work|[&work(replaceall,AC,[&emoShield])]
  --&work|[&work(replaceall,HP,[&emoHeart])]
  --&work|[&work(replaceall,SPEED,[&emoSpeed])]
  --&[%1%]|[&work]
  --<|

  --:YOU_CANNOT_DO_THAT| Used when someone other than the original sender tries to use the repeat button
  --#whisper|self
  --#title|You Can't Do That!
  --#emotetext|
  --#leftsub|
  --#rightsub|
  --+|You aren't the original sender of this action, so you can't use the repeat button
  --X|

  --:DISPLAY_PC_OTHER_ACTIONS|
    --/|Traits
    --RFirst|[*S:character_id];repeating_traits
    --&BA_BUTTON_LIST|
    --&RA_BUTTON_LIST|
    --?"[*R:name]" -ne "NoRepeatingAttributeLoaded"|[
    --:BA_TRAITS_BUTTON_LIST_LOOP|
      --~desc|string;replaceall;";;[*R:description] --/|" My editor screams at me if I don't close my quotes
      --?"[&desc]" -inc "bonus action"|&BA_BUTTON_LIST;+[rbutton][*R:name]::EXEC_PC_TRAIT;[*R:name][/rbutton][&SPACE]
      --?"[&desc]" -inc "reaction"|&RA_BUTTON_LIST;+[rbutton][*R:name]::EXEC_PC_TRAIT;[*R:name][/rbutton][&SPACE]
      --Rnext|
      --?"[*R:name]" -ne "NoRepeatingAttributeLoaded"|BA_TRAITS_BUTTON_LIST_LOOP
    --]|
    --&BA_BUTTON_LIST|+[rbutton]Two-Weapon Fighting::TWO_WEAPON_FIGHTING[/rbutton][&SPACE]
    --&RA_BUTTON_LIST|+[rbutton]Opportunity Attack::OPP_ATTACK[/rbutton][&SPACE]
    --+Bonus Actions|
    --+|[&BA_BUTTON_LIST]
    --+Reactions|
    --+|[&RA_BUTTON_LIST]
  --<|

  --:EXEC_PC_TRAIT|
  --#whisper|self
  --#emotestate|visible
  --Rfind|[*S:character_id];[&reentryval];repeating_traits;name
  --#title|[*R:name]
  --#leftsub|[*S:character_name]
  --#rightsub|
  --+Source|[i][*R:source] - [*R:source_type][/i]
  --+|[*R:description]
  --X|

  --:TWO_WEAPON_FIGHTING|
  --#whisper|self
  --#emotestate|nope
  --#title|Two-Weapon Fighting
  --#leftsub|[*S:character_name]
  --#rightsub|
  --+Source|[i]General[/i]
  --+|When you take the Attack action and attack with a light melee weapon that you're holding in one hand, you can use a bonus action to attack with a different light melee weapon that you're holding in the other hand. You don't add your ability modifier to the damage of the bonus attack, unless that modifier is negative.  
  --+|[BR]
  --+|If either weapon has the thrown property, you can throw the weapon, instead of making a melee attack with it.
  --X|

  --:OPP_ATTACK|
  --#whisper|self
  --#emotestate|nope
  --#title|Opprounity Attack
  --#leftsub|[*S:character_name]
  --#rightsub|
  --+Source|[i]General[/i]
  --+|In a fight, everyone is constantly watching for a chance to strike an enemy who is fleeing or passing by. Such a strike is called an opportunity attack.
  --+|[BR]
  --+|You can make an opportunity attack when a hostile creature that you can see moves out of your reach. To make the opportunity attack, you use your reaction to make one melee attack against the provoking creature. The attack occurs right before the creature leaves your reach.
  --+|[BR]  
  --+|You can avoid provoking an opportunity attack by taking the Disengage action. You also don't provoke an opportunity attack when you teleport or when someone or something moves you without using your movement, action, or reaction. For example, you don't provoke an opportunity attack if an explosion hurls you out of a foe's reach or if gravity causes you to fall past an enemy.
  --X|  

  --:CHECK_VERSION_INFO|
  --/?"[&SC_VERSION_NUMERIC]" -eq "" -or "[&SC_VERSION_NUMERIC]" -lt 207150|REPORT_BAD_SC_VERSION
  --<|
}}